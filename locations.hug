!locations.hug
room Hallway02 "Hallway Module"
{
  long_desc
  {
    if self is not visited
    {
      move Captain to location
      move Finnian to location
      remove latte
    }

    "There is a brief hallway module connecting the Captain's bedroom to the hold, which is up ahead."

    if self is not visited
    {
      "\n\"Not to be a pest, dear,\" starts Finnian to the Captain, \"but do you think we should put some form of
      self-defense on the solar itinerary?\"\n"

      if xraySpecs is not special
      {
        "The Captain smiles at him without using her eyes. She doesn't directly address him, which is something she didn't used to do,
        but something I noticed her doing when I came back from leave. \"I think Ensign Butler lowering his x-ray specs to give
        us some help on what's ahead would be the next best tactic,\"she says, genuinely sweetly.\n"

        "Ensign Butler! Oops, that's me, and yes, as the x-ray technician of the Plagoo, I carry some specs with me.
        I can put my specs down."
      }
      else
      {
        "The Captain smiles humorously. \"I think we're headed on a good path. We've a weapon upcoming in the hold and Ensign Butler
        has his x-ray specs deployed in the scan position to let us know what's ahead.\" The Captain has a talent for seeming like
        she's in control of the situation, which is nice, because I don't need augmented vision to know my heart is pounding. Although
        I guess I really shouldn't stare at human bodies when the specs are engaged for too long."
      }

    }
  }

  xrayDescription {
    if Captain in location
    {
      "\"There's nothing out of the ordinary to report for the next room ahead, Captain - no werewolf, nothing particular crazy.\""
    }
    else {
      "Nothing to report here."
    }
  }
e_to HallwayDoor
w_to Hallway03
}

room Hallway03 "Ship Hold"
{
  long_desc
  {
    move Captain to location
    move Finnian to location

    if self is not visited
    {
      PlayMusic("ENCEL01", "JESTER - Stardust Memories", 70, true, true)
      move starPistol to Finnian
      "The Plagoo has a small hold right off the Captain's bedroom, and this is it. The original owner of the ship didn't trust his crew
      with the cargo they ran with. Our Captain does, but the Plagoo can't exactly be reconfigured.\n"
    }

    "The hold itself normally has poor lighting, it's even worse now that lighting systems have apparently been damaged. It's rectangular
    in shape, maybe ten feet wide and fifteen feet long. The walls were painted a deep maroon like much of the rest of the interior at
    one point. There's a great deal of pallets stacked on both sides."

    if self is not visited
    {
      "\nThe Captain opens one of the packages on top of a pallet to my right. It's an unopened star pistol. She unboxes it, avoids
      cursing at the tight grip plastic keeping the pistol in place and inserts the battery into the grip with a solid 'twack.' She
      hands it to Finnian.\n"

      "\"Let me do the talking if it gets that far, I've got an idea what is going on,\" she says to Finnian. \"I'd say don't draw
      attention to yourself, but, well.\" She gestures at the hammock. \"Keep the pistol hidden.\""

      Activate(FinnianGivesPistol)
    }

    "\nExits lead west, further into the ship, and east, back down the hallway where we came."

  }
xrayDescription {
        GameEvents[7] = 1
        "I can see ahead that there's actually two people waiting for us in the next area.\n"
        "\"Captain, I'm seeing two unknown forms and what I think is Alexandra.\" Alexandra is our naviagor and the life support tech. \"Alexandra
        seems to be in the fetal position next to the air lock. One of the forms definitely looks like the silouette of a werewolf. The other
        form standing next to it is taller, has lead boots. No weapons that I can detect. I also think that Grimes's robot is there.\""

        if GameEvents[8] = 0
        {
          GameEvents[8] = 1
          "\n\"Good work,\" says the Captain. \"Let's proceed.\""
        }

}

w_to {
  if GameEvents[5] = 0 !// We need Finnian to give Ja'Rod the star pistol
  {
    "I should wait for the Captain to tell us to proceed instead of just running into trouble." ;

    if GameEvents[6] = 0
    {
      GameEvents[6] = 1

      " Not trying to be the softest serve on the ship, it's just that this is a learning experience for me, regarding werewolves on the ship."

    }
    else {
      "" !// Nothing else
    }
  }
  else {
    !// We need Ja'Rod to look at what is ahead
    if GameEvents[7] = 1
    {
      return Hallway04
    }
    else {
      "I can't go that way yet. The Captain wants me to lower my x-ray specs and take a look so we can see what's ahead."
    }
  }
}
e_to Hallway02
}

room Hallway04 "Monitoring"
{
  long_desc
  {
    move Captain to location
    move Finnian to location

    "This is one room before the airlock, where I saw two figures next to Alexandra, who is part of our crew, and Mr Mailx, who
    is the robot that Grimes got working."

    if self is not visited
    {
      "\n\"This is what I'd like us to do,\" says the Captain, \"when we proceed to the airlock up ahead. Finnian, I would like you to accompany me as we
      talk to our interlopers on the ship. Ja'Rod, I would like you to stay hidden and shoot the hull behind them when I snap my fingers.\"\n"

      "Finnian nods, the disposition he normally carries about has left for a moment. After all, there are enemies on the ship and we can still
      save Alexandra. I have a few thoughts - why not shoot the werewolf and the other guy instead of putting a hole in the ship, but I just nod as well."
    }
  }
w_to BeforeTheAirlock
e_to Hallway03
}

room BeforeTheAirlock "Before the Airlock"
{
long_desc
{

  if GameEvents[11] = 1
  {
    if GameEvents[17] = 0
    {
      GameEvents[17] = 1 !// Do this once, please!
      move Mailx to location
      move Alexandra to location
      Mailx.talkValue = 1
      Alexandra.talkValue = 1
      move greenButton to location
      move redButton to location
      Activate(Airlock_Checker)
    }

    "I'm before the airlock again. Mr Mailx is here plugging the hole I created. Alexandra is conscious, but laying on a blanket that the Captain somehow
    produced. "

  }
  else {
    move Captain to location
    move Finnian to location

    if self is not visited
    {
      self is hidden
    }

    if self is hidden
    {
      "(I'm hidden behind some cargo crates, but nobody else is.)\n"
    }
    if self is not visited
    {
      "We are in a narrow hallway, there are monitors mounted a couple meters high in both directions. Inside our airlock is Mr Mailx and Alexandra. Outside of
      the airlock is the werewolf and someone I don't recognize. The Captain and Finnian approach.\n"
      "\"Captain!\" says the unknown figure. \"How nice of you to join us. I plan on sending every member of your crew out the airlock.\"\n"
      "\"How did you get onto my ship?\" the Captain asks.\n"
      "\"Through the airlock. The other way. It seems you sent the unencrypted wireless key to Grimes when you hired him, and Grimes failed to secure
      his communicator.\"\n"
      "\"You came in through the airlock on the opposite side? Without that thing wearing a suit?\" She points to the werewolf.\n"
      "The werewolf's head is still pointed toward the Captain, but it turns bloodshot, vacuum-afflicted eyes towards the man after that for a long second
      before settling back onto the Captain and Finnian.\n"
      "\"It can take it!\" the man hisses. He notices Finnan for the first time. Desperate to change the subject, he addresses Fin. \"What are you wearing?\"\n"
      "\"Appropriate dress in this area of the solar system for shore leave, McGrathley,\" he says. McGrathley! I didn't recognize him in the spacesuit,
      but this is Riggs McGrathley, a pirate we've encountered once before."

      Activate(Airlock_Scene)
    }
    else {
      if self is hidden
      {
        "I'm hiding behind some crates in the room before our airlock. " ;

      }
      else {
        "This room is before our airlock and it's got a few scattered crates and a control panel along the portside wall."
      }
    }
  }
}

before
{
  location DoJump
  {
    "Not a great place to do that."
  }
}

s_to {
  !// Can we leave BeforeTheAirlock and go back towards the Bridge, in other words, is alexandraInAirlock = true and is mailxIsPulled = true?
  if GameEvents[14] = 0
  {
    "I can't go that way right now."
  }
  else {
    return PortSideHall02
  }
}
e_to { "I can't go that way right now."}
}

room Bridge "Bridge"
{
  long_desc
  {
    if self is not visited
    {
      "Finnian and I run back to the helm on the Bridge. The Captain stayed to check on Alexandra. Finnian doesn't ever really start any conversations with me.
      After working with him for two years and then a year break and then the last few weeks I have no idea if he likes me or not. It always seems
      like I am bothering him working when I bring up non-work topics, and it always seems like he thinks I'm tedious when I bring up work at
      any other time.\n"

      "\"That was crazy, huh?\" I say. Why not, what can it hurt.\n"

      "\"Well, the best part of that plan is that it would have also worked with Grimes's fat ass in the breach, not just his robot.\"
      Former employee bashing! I guess I'm off the hook for a bit.\n"

      "Finnian sits down and starts fiddling with the navigation controls."

      Activate(Finnian_Lands_Ship)

    }
    else {
      "This is the Bridge of our ship, there's a chair in the middle of the room for the Captain. Finnian has a navigator's console before a
      large black high definition television. I have a station behind the Captain port side."

      if mailxIsPulled = true and alexandraInAirlock = true
      {
       move Mailx to location
       "\n\"Ensign Butler,\" starts the Captain, \"How is our life support technician doing?\"\n"
       "\"Alexandra is inside the airlock, the temperature controls are active and she'll be safe there. That room is getting cold.\n"
       "The Captain nods. \"Very well. Let's - the four of us - meet outside for our next steps.\n"
       "[Strike any key to continue]"
       pause
       StartPlanetFallScene()
      }

    }
  }

  before {
    location DoExits
    {
      if GameEvents[12] = 0
      {
        "There is an exit north to the hall but I shouldn't leave my post at the moment."
      }
      else {
        "There is an exit north into the hall."
      }

      }
  }

n_to  {
    if GameEvents[12] = 0
    {
      "I should stay here in the Bridge for a bit."
    }
    else {
      return PortSideHall01
    }
}
}

room PortSideHall01  "Port Side Hallway"
{
  long_desc
  {
    if self is not visited
    {
    	PlayMusic("ENCEL01", "JESTER - Wizardry", 70, true, true)
      "This is just a simple access hallway that allows me to get to different areas of the ship.\n"
      "I can tell that Finnian has initiated the ship landing procedures, there is a whirring and clicking and sounds of orchestral strikes.
      Some of the latter may simply be that Finnian is force-piping the album Big Generator through the ship's audio, however."
    }
    else {
      "This is just a simple access hallway that allows me to get to different areas of the ship.\n"

      if alexandraInAirlock = true and mailxIsPulled = true
      {
        move Mailx to location
      }
    }
  }

s_to Bridge
n_to PortSideHall02
}

room PortSideHall02 "PortSide Hallway"
{
  long_desc
  {
    if self is not visited
    {
      GameEvents[11] = 1 !//  Is this our second time in the Before the Airlock room?
      "To the north is the airlock and to the south is more hallway."
    }
    else {
      "To the south is more hallway, but eventually the bridge."
    }

    if alexandraInAirlock = true and mailxIsPulled = true
    {
      move Mailx to location
   }
  }

s_to PortSideHall01
n_to  {
  if mailxIsPulled = true and alexandraInAirlock = true
  {
    "I don't have any reason to go back that way, I should go back towards the Bridge."
  }
  else {
  return  BeforeTheAirlock
  }
}
}

room OutsideTheShip "Outside the Plagoo"
{
  long_desc
  {
    if self is not visited
    {
      PlayMusic("JOGEIR LILJEDAHL - G-Comp", 80, true, true)

      move Finnian to SnowyPath01
      move Mailx to SnowyPath01
      move Captain to SnowyPath01

      Deactivate(Airlock_Checker)

      "Finnian looks around before looking back toward the group. He changed before we came down, now wearing a real Lynx fur coat, a white dunce cap and new
      old stock original Air Jordans. \"Crap, Captain, it's an ice planet!\"\n"
      "She nods at that. \"Intelligence indicates that there's a small town a kilometer east of here.\"\n"
      "\"What's your source of that intelligence?\" asks Finnian.\n"
      "The Captain grits her teeth. \"I consulted Wikipedia. Okay? Is that all right?\"\n"
      "\"No it's not all right!\" says Finnian. \"Wikipedia's moderator and administration system is filled with duplicitous losers that turned the
      exchange of free information into a political battle where scumbags scuttle into tribes to spread their lies. Mr Mailx?\"\n"
      "\"Yes,\" he says.\n"
      "\"Delete the Wikipedia page on Enceladus and replace it with a 10K image of a giant tarantula.\"\n"
      "\"Finnian, stop,\" says the Captain.\n"
      "\"Done,\" says the robot, presumably having fired off some API calls.\n"
      "Finnian laughs. It's a dry, hollow one. \"Seventy one minutes from now, good luck to any kid trying to do a book report on Jovian moons. Cite
      that revision in your bibliography.\"\n"

      "The Captain walks east. Mr Mailx starts up to join her and after a shrug and then a replacing of his precariously-set hat, Finnian goes east too."
    }
    else {
      "I am outside the HMCS Plagoo, although the ship is locked and the walkway up is closed. The other three members of my party have gone east. This
      is my first time on Enceladus, the sixth moon of Saturn. The gravity is a lot lower than Earth or the ship, although a injector has been installed
      in the moon's core to give it a little more pull than it had naturally. Virtually the entire surface is covered with ice, or (where people have
      set up civilization) crushed ice."
    }
  }
  e_to SnowyPath01
}

room SnowyPath01 "Snowy Path"
{
  long_desc
  {
    if self is not visited
    {
      move Finnian to SnowyPath02
      move Mailx to SnowyPath02
      move Captain to SnowyPath02
      "\"... I'm just saying, have you ever tried to edit something on it? No? Oh, hey Ja'Rod,\" says Finnian.\n"
      "\"Our plan is going to be this,\" says the Captain. \"There's a hot sauce bar up ahead. We ask around to find a doctor. Since
      we also need to repair the ship, let's see if we can find a mine.\"\n"
      "We all nod at this except for Mr Mail, who just keeps treading east with a low whir."
    }
    else {
      "I am on a snowy path towards civilization on the moon Enceladus."
    }
  }

w_to OutsideTheShip
e_to SnowyPath02
}

room SnowyPath02 "Snowy Path"
{
  long_desc
  {
    if self is not visited
    {
      "\"... bring her back something to drink is all I'm saying, they can't just have hot sauce. Maybe a bottle of scotch to anesthetize her wound. Hey robot,
      does scotch kill the bacteria and germs around wounds?\"\n"
      "\"No,\" says Mr Mailx.\n"
      "\"What does he know, he spends his free time vandalizing encyclopedias,\" Fin says.\n"
      "\"What do you think the word anesthetize means?\" asks the Captain, who is still walking. For the first time I understand what
      Finnian has been trying to do lately, he's trying to \Bcheer her up.\b It isn't working, because he's a socially maladjusted narcissist,
      but he's trying.\n"
      "\"Ja'Rod,\" she says, \"Can you give me a scan of what is up ahead?\""

      Activate(Water_Mists)
    }
    else {

      "Where we happen to be on Enceladus is a place on the moon that is on the opposite side of Saturn. I have heard it's a bit overwhelming when
      you're on the other side of the moon and Saturn is there looking at you like a giant angry god. The way Enceladus moves in its orbit, we
      won't ever get that view which is bad from a life experience standpoint but better from an anxiety-inducing one, I think.\n"

      "The ground is pure ice, it isn't clear to me if humans or machines have ever truly walked on this particular part of the moon ever.
      Very slippery."
    }
  }

  xRayDescription {

    if GameEvents[16] = 0
    {
      "\"I see giant spiders, tell Mr Mailx to take some on location shots for the next batch of edits,\" I say, trying to keep the conversation going. \"No,
      for real though, there is, sure enough, a bar not too far away and it's across from the entrance to an underground mine. I'm not getting
      good readings on what sort of metals are down there, so I assume it's lead and something else.\""

      GameEvents[16] = 1
    }
    else {
      "Up ahead is a bar and across the street from it is an underground mine."
    }
  }

  e_to {
    if GameEvents[16] = 0
    {
      "I need to do a scan first, as requested by the Captain."
    }
    else {
      return SnowyPath03
    }
  }

}

room SnowyPath03 "Snowy Path"
{
  long_desc
  {
    if self is not visited
    {
      move Finnian to location
      move Captain to location
      move Mailx to location

      "\"Okay, so we need a doctor to come back to the ship,\" says Finnian. \"D'ya think they'll just come because Alexandra is hurt? That would make it sort of a house call, wouldn't it? I don't know
      if doctors on this moon do house calls.\"\n"
      "\"I don't think we should bring up payment until whoever we find does, \" says the Captain. \"And if they do, let's let them throw out a number first.\"\n"
      "\"Ah-ha!\" exclains Finnian. \"So good to be on the other side of your negotiations for a chance. I see how it goes now. This mystery doc will be lucky to have an eighth share.\"\n"
      "\"None of you are at an eighth of a share,\" replies the Captain.\n"
      "\"The robot is. Hey robot, you getting an eighth of a share?\"\n"
      "\"No,\" says Mr Mailx.\n"
      "\"There ya go. Too bad you look like three poorly stacked Commodores. If the type of machine you were was a Rug Doctor the Captain might have paid you for this.\" Finnian waves his hands. \"All
      of this.\"\n"

      "I see now that we landed in just an enormous flat crater -- the crater's edge is just beyond this little section of town. I still have no idea where anyone around here lives. I'm hoping their
      homes are out of the crater and they all get around by jetpack. (There is what appears to be an abandoned snowmobile close by with tracks leading to the bar, so maybe it's just that.)"

    }
    else {
      "I'm inside the far edge of a crater here on Enceladus. The crater's walls are visible now, maybe no more than another kilometer. The whole moon is only a quarter the size of Ethiopia, where
      I grew up. A disused snowmobile lies a bit away, and up ahead, to the east, is the hot sauce bar and the entrance to a mine."
    }
  }

  e_to CenterOfTown
  w_to SnowyPath02
}

room CenterOfTown "Center of Town"
{
  long_desc
  {
    if self is not visited
    {
      move Finnian to location
      move Captain to location
      move Mailx to location

      "There is a large log cabin to the south. Pretty rustic. Trees don't exactly grow here, so the fact that it exists at all means that someone really wanted to have an earthly experience. To the north
      is a cave, my scan indicated that people can descend downwards.\n"

      "\"I can do the talking once we g-- \" starts Finnian.\n"
      "\"I'll do the talking,\" interrupts the Captain.\n"
      "\"I'll try the milder hot sauces,\" I add, helpfully."
    }
    else {

      if GameEvents[15] = 1 !// Did Ja'Rod cut himself or otherwise find the doctor?
      {
        !// This means we have sent the doctor and Finnian off.
        !// We need to have the Captain tell Finnian and N
      }
    }
  }

s_to HotSauceBar
w_to SnowyPath03
n_to {
  if GameEvents[19] = 0 !// Did Ja'Rod and the Captain find a doctor?
  {
    "I can't go that way just yet. Valid exits are to the south and west."
  }
  else {
    return EntranceToMine
  }
}

}

room HotSauceBar "Hot Sauce Bar"
{
long_desc
{
  if self is not visited
  {
    move Captain to location

    "The four of us enter the bar and we're greeted by a doorman. He is suited up in a caribou skin and fur coat. I don't think it was made for him as he's sitting on a stool and the thing
    falls all over the floor of the bar like a wedding dress.\n"
    "\"You can't bring that thing in here,\" says the bouncer, but he is pointing to Finnian. Mr Mailx turns his entire body to face Finnian, which is the creepiest thing ever because
    of how long it takes him to do that on tread.\n"
    "\"That's my navigator,\" says the Captain.\n"
    "\"There's one room and a bathroom in here, you'll be able make your way around just fine,\" says the bouncer. \"Get that cap out of here before I throw you all out myself.\"\n"
    "\"Oh, it's the cap?\" asks Finnian. \"This thing is the height of fashion far back around the outer moons of Saturn.\" The Captain rolls her eyes.\n"
    "\"This is a moon of Saturn, dummy,\" says the bouncer.\n"
    "Finnian looks genuinely surprised at that revelation. \"Huh,\" he says. \"Come on, robot,\" he says to Mr Mailx. The two of them leave, to presumably wait outside or try to find a print
    encyclopedia to assert the correctness of the bouncer's claim.\n"

    "Now that we're inside, this looks just like any other bar in the solar system, except colder and with a scent of chile peppers instead of old hops. There are a series of booths around the
    walls with the bar itself a square right in the middle. I'd say there's probably thirty or forty men, women and robots in here.\n"

    "The Captain points to a couple empty stools near the plastic bin of lemons and limes. \"Shall we?\" she asks."

  }
  else {

  }
}

n_to {
  if GameEvents[19] = 0
  {
    "I can't leave yet, I have to help find a doctor."
  }
  else {
    return CenterOfTown
  }
}
}

room EntranceToMine "Mine Entrance"
{
long_desc
{
  if self is not visited
  {
  !// Need to mention how the Captain got the key and why she gave it to Ja'Rod

  }
  else {

  }
}
}

door MineDoor "door"
{
  nouns "door"
  article "the"
  between EntranceToMine, mine
  is locked
  key_object mineKey

  long_desc
  {
    !// [xxx]

  }
}


room mine "Mine"
{
long_desc
{
  if self is not visited
  {
    !// [xxx]
  }
  else {
    !// [xxx]
  }
}

d_to mineLevelTwo
}

room mineLevelTwo "Mine Level Two"
{
  long_desc
  {
    !// [xxx]
  }

u_to "We can't go that way by ourselves."
d_to mineLevelThree
}

room mineLevelThree "Mine Level Three"
{
  long_desc
  {

  }

! Mine Cart is in here and can take us up.

u_to "We can't go that way by ourselves."
e_to bottomOfMine
}

room bottomOfMine "Bottom of Mine"
{
  long_desc
  {

  }

u_to "We can't go that way by ourselves."
w_to {
  if GameEvents[20] = 0
  {
    "I can't go that way due to the cave-in."
  }
  else {
    return mineLevelThree
  }
}
}

room RecRoom "Rec Room"
{

}
