!locations.hug
room Hallway02 "Hallway Module"
{
  long_desc
  {
    if self is not visited
    {
      move Captain to location
      move Finnian to location
      remove latte

      TheCaptainsQuarters.name = "The Captain's Quarters" !// Remove the bit about us being on the Plagoo
    }

    "There is a brief hallway module connecting the Captain's bedroom to the hold, which is up ahead."

    if self is not visited
    {
      "\n\"Not to be a pest, dear,\" starts Finnian to the Captain, \"but do you think we should put some form of
      self-defense on the solar itinerary?\"\n"

      if xraySpecs is not special
      {
        "The Captain smiles at him without using her eyes. She doesn't directly address him, which is something she didn't used to do,
        but something I noticed her doing when I came back from shore leave. \"I think Ensign Butler lowering his X-ray specs to give
        us some help on what's ahead would be the next best tactic,\"she says, genuinely sweetly.\n"

        "Ensign Butler! Oops, that's me, and yes, as the X-ray technician of the Plagoo, I carry some specs with me.
        I can \Blower my specs\b and perform a \Bscan\b with them."

        xraySpecs is not hidden
      }
      else
      {
        "The Captain smiles humorously. \"I think we're headed on a good path. We've a weapon upcoming in the hold and Ensign Butler
        has his x-ray specs deployed in the scan position to let us know what's ahead.\" The Captain has a talent for seeming like
        she's in control of the situation, which is nice, because I don't need augmented vision to know my heart is pounding. Although
        I guess I really shouldn't stare at human bodies when the specs are engaged for too long."

        xraySpecs is not hidden
      }

    }
  }

  xrayDescription {
    if Captain in location
    {
      "\"There's nothing out of the ordinary to report for the next room ahead, Captain - no werewolf, nothing particular crazy.\""
    }
    else {
      "Nothing to report here."
    }
  }
e_to HallwayDoor
w_to Hallway03
}

room Hallway03 "Ship Hold"
{
  long_desc
  {
    move Captain to location
    move Finnian to location

    if self is not visited
    {
      PlayMusic("ENCEL01", "JESTER - Stardust Memories", 70, true, true)
      MusicListing = 6

      move starPistol to Finnian

      Activate(Hallyway03_Scan_Checker)

      "The Plagoo has a small hold right off the Captain's bedroom, and this is it. The original owner of the ship didn't trust his crew
      with the cargo they ran with. Our Captain does, but the Plagoo can't exactly be reconfigured.\n"
    }

    "The hold itself normally has poor lighting, it's even worse now that lighting systems have apparently been damaged. It's rectangular
    in shape, maybe ten feet wide and fifteen feet long. The walls were painted a deep maroon like much of the rest of the interior at
    one point. There's a great deal of pallets stacked on both sides."

    if self is not visited
    {
      "\nThe Captain opens one of the packages on top of a pallet to my right. It's an unopened star pistol. She unboxes it, avoids
      cursing at the tight grip plastic keeping the pistol in place and inserts the battery into the grip with a solid 'twack.' She
      hands it to Finnian.\n"

      "\"Let me do the talking if it gets that far, I've got an idea what is going on,\" she says to Finnian. \"I'd say don't draw
      attention to yourself, but, well.\" She gestures at the hammock. \"Keep the pistol hidden.\""

      Activate(FinnianGivesPistol)
    }

    "\nExits lead west, further into the ship, and east, back down the hallway where we came."

  }
  before
  {
    location DoExits
    {
      "I can go west and east from here."
    }
  }
xrayDescription {
        GameEvents[7] = 1
        Deactivate(Hallyway03_Scan_Checker)

        "I can see ahead that there's actually two people waiting for us in the next area.\n"
        "\"Captain, I'm seeing two unknown forms and what I think is Alexandra.\" Alexandra is our naviagor and the life support tech. \"Alexandra
        seems to be in the fetal position next to the air lock. One of the forms definitely looks like the silouette of a werewolf. The other
        form standing next to it is taller, has lead boots. No weapons that I can detect. I also think that Grimes's robot is there.\""

        if GameEvents[8] = 0
        {
          GameEvents[8] = 1
          "\n\"Good work,\" says the Captain. \"Let's proceed.\""
        }

}

w_to {
  if GameEvents[5] = 0 !// We need Finnian to give Ja'Rod the star pistol
  {
    "I should wait for the Captain to tell us to proceed instead of just running into trouble." ;

    if GameEvents[6] = 0
    {
      GameEvents[6] = 1

      " Not trying to be the softest serve on the ship, it's just that this is a learning experience for me, regarding werewolves on the ship."

    }
    else {
      "" !// Nothing else
    }
  }
  else {
    !// We need Ja'Rod to look at what is ahead
    if GameEvents[7] = 1
    {
      return Hallway04
    }
    else {
      "I can't go that way yet. The Captain wants me to lower my X-ray specs and perform a \Bscan\b so we can see what's ahead."
    }
  }
}
e_to Hallway02
}

room Hallway04 "Monitoring"
{
  long_desc
  {
    move Captain to location
    move Finnian to location

    "This is one room before the airlock, where I saw two figures next to Alexandra, who is part of our crew, and Mr Mailx, who
    is the robot that Grimes got working."

    if self is not visited
    {
      "\n\"This is what I'd like us to do,\" says the Captain, \"when we proceed to the airlock up ahead. Finnian, I would like you to accompany me as we
      talk to our interlopers on the ship. Ja'Rod, I would like you to stay hidden and shoot the hull behind them when I snap my fingers.\"\n"

      "Finnian nods, the disposition he normally carries about has left for a moment. After all, there are enemies on the ship and we can still
      save Alexandra. I have a few thoughts - why not shoot the werewolf and the other guy instead of putting a hole in the ship, but I just nod as well."

      Captain.talkValue = 3
      Finnian.talkValue = 999
    }
  }
w_to BeforeTheAirlock
e_to Hallway03
}

room BeforeTheAirlock "Before the Airlock"
{
long_desc
{
  if GameEvents[11] = 1
  {
    if GameEvents[17] = 0
    {
      GameEvents[17] = 1 !// Do this once, please for setting up Before the Airlock
      move Mailx to location
      move Alexandra to location
      Mailx.talkValue = 1
      Alexandra.talkValue = 1
      move greenButton to location
      move redButton to location
      Activate(Airlock_Checker)
    }

    "I'm before the airlock again. Mr Mailx is here plugging the hole I created. Alexandra is conscious, but laying on a blanket that the Captain somehow
    produced. "

    move airlock to BeforeTheAirlock
    move blanket to BeforeTheAirlock
  }
  else {
    move Captain to location
    move Finnian to location

    if self is not visited
    {
      self is hidden
      Captain.talkValue = 999
      Finnian.talkValue = 999
      move Alexandra to location
      move Mailx to location
    }

    if self is hidden
    {
      "(I'm hidden behind some cargo crates, but nobody else is.)\n"
    }
    if self is not visited
    {
      "We are in a narrow hallway, there are monitors mounted a couple meters high in both directions. Inside our airlock is Mr Mailx and Alexandra. Outside of
      the airlock is the werewolf and someone I don't recognize. The Captain and Finnian approach.\n"
      "\"Captain!\" says the unknown figure. \"How nice of you to join us. I plan on sending every member of your crew out the airlock.\"\n"
      "\"How did you get onto my ship?\" the Captain asks.\n"
      "\"Through the airlock. The other way. It seems you sent the unencrypted wireless key to Grimes when you hired him, and Grimes failed to secure
      his communicator.\"\n"
      "\"You came in through the airlock on the opposite side? Without that thing wearing a suit?\" She points to the werewolf.\n"
      "The werewolf's head is still pointed toward the Captain, but it turns bloodshot, vacuum-afflicted eyes towards the man after that for a long second
      before settling back onto the Captain and Finnian.\n"
      "\"It can take it!\" the man hisses. He notices Finnian for the first time. Desperate to change the subject, he addresses Fin. \"What are you wearing?\"\n"
      "\"Appropriate dress in this area of the solar system for shore leave, McGrathley,\" he says. McGrathley! I didn't recognize him in the spacesuit,
      but this is Riggs McGrathley, a pirate we've encountered once before."

      Activate(Airlock_Scene)
    }
    else {
      if self is hidden
      {
        "I'm hiding behind some crates in the room before our airlock. " ;

      }
      else {
        "This room is before our airlock and it's got a few scattered crates and a control panel along the portside wall."
      }
    }
  }
}
before
{
  location DoJump
  {
    "Not a great place to do that."
  }
  location DoExits
  {
    if GameEvents[24] = 0
    {
      "I can't really leave right now."
    }
    else {
      "I can go south back to the Bridge."
    }
  }
}
s_to {
  !// Can we leave BeforeTheAirlock and go back towards the Bridge, in other words, is alexandraInAirlock = true and is mailxIsPulled = true?
  if GameEvents[14] = 0
  {
    "I can't go that way right now."
  }
  else {
    return PortSideHall02
  }
}
e_to { "I can't go that way right now."}
}

room Bridge "Bridge"
{
  long_desc
  {
    if self is not visited
    {
      "Finnian and I run back to the helm on the Bridge. The Captain stayed to check on Alexandra. Finnian doesn't ever really start any conversations with me.
      After working with him for two years and then a year break and then the last few weeks I have no idea if he likes me or not. It always seems
      like I am bothering him working when I bring up non-work topics, and it always seems like he thinks I'm tedious when I bring up work at
      any other time.\n"

      "\"That was crazy, huh?\" I say. Why not, what can it hurt.\n"

      "\"Well, the best part of that plan is that it would have also worked with Grimes's fat ass in the breach, not just his robot.\"
      Former employee bashing! I guess I'm off the hook for a bit.\n"

      "Finnian sits down and starts fiddling with the navigation controls."

      Activate(Finnian_Lands_Ship)

      GameEvents[24] = 1 !// This is for the DoExits in BeforeTheAirlock
      Finnian.talkValue = 2
    }
    else {
      "This is the Bridge of our ship, there's a chair in the middle of the room for the Captain. Finnian has a navigator's console before a
      large black high definition television. I have a station behind the Captain port side."

      if mailxIsPulled = true and alexandraInAirlock = true
      {
       move Mailx to location
       Mailx.talkValue = 999

       "\nI hear the Captain via the communications system on the Plagoo.\n"
       "\"Ensign Butler,\" she says, \"How is Alexandra?\"\n"
       "\"Alexandra is inside the airlock, the temperature controls are active and she'll be safe there. That room is getting cold.\n"
       "\"Very well. Let's meet outside for our next steps.\""

       "\n[Strike any key to continue]"

       pause

       StartPlanetFallScene()
      }
    }
  }
  before {
    location DoExits
    {
      if GameEvents[12] = 0
      {
        "There is an exit north to the hall but I shouldn't leave my post at the moment."
      }
      else {
        "There is an exit north into the hall."
      }
    }
  }
n_to  {
    if GameEvents[12] = 0
    {
      "I should stay here in the Bridge for a bit."
    }
    else {
      return PortSideHall01
    }
}
}

room PortSideHall01  "Port Side Hallway"
{
  long_desc
  {
    if self is not visited
    {
    	PlayMusic("ENCEL01", "JESTER - Wizardry", 70, true, true)
      MusicListing = 5

      "This is just a simple access hallway that allows me to get to different areas of the ship.\n"
      "I can tell that Finnian has completed the ship landing procedures, there is a whirring and clicking and sounds of orchestral strikes.
      (Some of the latter may simply be that Finnian is force-piping the album Big Generator through the ship's audio, however.)"
    }
    else {
      "This is just a simple access hallway that allows me to get to different areas of the ship."

      if alexandraInAirlock = true and mailxIsPulled = true
      {
        move Mailx to location
      }
    }
  }
  before
  {
    location DoListen
    {
      "Just the whirring of the ship itself and " ;
      testValue = random(8)
      select(testValue)
      case 1 {"'Love Will Find a Way.''" }
      case 2 {"'Rhythm of Love.'" }
      case 3 {"'Big Generator, the single.'"}
      case 4 {"'Shoot High Aim Low.'"}
      case 5 {"'Almost Like Love.'"}
      case 6 {"'Final Eyes.'"}
      case 7 {"'I'm Running.'"}
      case else {"'Holy Lamb.'"}
    }
  }

s_to Bridge
n_to PortSideHall02
}

room PortSideHall02 "PortSide Hallway"
{
  long_desc
  {
    if self is not visited
    {
      GameEvents[11] = 1 !//  Is this our second time in the Before the Airlock room?
      "To the north is the airlock and to the south is more hallway."
    }
    else {
      "To the south is more hallway, but eventually the bridge."
    }

    if alexandraInAirlock = true and mailxIsPulled = true
    {
      move Mailx to location
   }
  }

s_to PortSideHall01
n_to  {
  if mailxIsPulled = true and alexandraInAirlock = true
  {
    "I don't have any reason to go back that way, I should go back towards the Bridge."
  }
  else {
  return  BeforeTheAirlock
  }
}
}

room OutsideTheShip "Outside the Plagoo"
{
  long_desc
  {
    if self is not visited
    {
      PlayMusic("ENCEL01", "JOGEIR LILJEDAHL - G-Comp", 80, true, true)
      MusicListing = 7

      move Finnian to SnowyPath01
      move Mailx to SnowyPath01
      move Captain to SnowyPath01

      GameEvents[28] = 1 !// For Finnian's clothes
      GameEvents[29] = 1 !// For Captain's description

      Deactivate(Airlock_Checker)

      remove thong
      move dunceCap to Finnian
      move furCoat to Finnian
      move airJordans to Finnian

      "Finnian looks around before looking back toward the group. He changed before we came down, now wearing a real Lynx fur coat, a white dunce cap and new
      old stock original Air Jordans. \"Crap, Captain, it's an ice planet!\"\n"
      "She nods at that. \"Intelligence indicates that there's a small town a kilometer east of here.\"\n"
      "\"What's your source of that intelligence?\" asks Finnian.\n"
      "The Captain grits her teeth. \"I consulted Wikipedia. Okay? Is that all right?\"\n"
      "\"No it's not all right!\" says Finnian. \"Wikipedia's moderator and administration system is filled with duplicitous losers that turned the
      exchange of free information into a political battle where scumbags scuttle into tribes to spread their lies. Mr Mailx?\"\n"
      "\"Yes,\" he says.\n"
      "\"Delete the Wikipedia page on Enceladus and replace it with a 10K image of a giant tarantula.\"\n"
      "\"Finnian, stop,\" says the Captain.\n"
      "\"Done,\" says the robot, presumably having fired off some API calls.\n"
      "Finnian laughs. It's a dry, hollow one. \"Seventy one minutes from now, good luck to any kid trying to do a book report on Jovian moons. Cite
      that revision in your bibliography, you sniveling Webelo punks.\"\n"

      "The Captain walks east. Mr Mailx starts up to join her and after a shrug and then a replacing of his precariously-set hat, Finnian goes east too."
    }
    else {

      if GameEvents[20] = 1
      {
        PlayMusic("ENCEL01", "JOGEIR LILJEDAHL - Physical Presence", 80, true, true)
        MusicListing = 8

        GameEvents[20] = 2 !// Do this once

        move werewolf to location
        move McGrathley to location
        remove Alexandra
        remove Finnian
        remove MiladNahas
        move starPistol to Captain
        starPistol is hidden

        "McGrathley and the werewolf are here, it appears that they are trying to hack into the boarding ramp of the Plagoo in order to board it.\n"

        "Upon seeing us, they put their handheld scanner away. The werewolf begins snarling, having recognized us, but it is waiting for a command
        to strike.\n"

        "\"The cave-in should have crushed you, Captain,\" says McGrathley. \"Enough, I'll just kill you myself.\" He produces a revolver, an old
        fashioned six shot. The Captain disembarks the snowmobile and slowly begins walking toward him.\n"
        "McGrathley fires once. The bullet misses the Captain by about twenty feet and reaches escape velocity, leaving Enceladus until it is presumably
        captured by the gravity of Saturn.\n"

        "I roll off the snowmobile and stand towards the Plagoo. The werewolf begins scrambling toward me."

        Activate(Captain_Shoots_McGrathley)
      }
      else {
        "I am outside the HMCS Plagoo, although the ship is locked and the walkway up is closed. The other three members of my party have gone east. This
        is my first time on Enceladus, the sixth moon of Saturn. The gravity is a lot lower than Earth or the ship, although a injector has been installed
        in the moon's core to give it a little more pull than it had naturally. Virtually the entire surface is covered with ice, or (where people have
        set up civilization) crushed ice."
      }

    }
  }
  e_to SnowyPath01
}

room SnowyPath01 "Snowy Path"
{
  long_desc
  {
    if self is not visited
    {
      move Finnian to SnowyPath02
      move Mailx to SnowyPath02
      move Captain to SnowyPath02
      "\"... I'm just saying, have you ever tried to edit something on it? No? Oh, hey Ja'Rod,\" says Finnian.\n"
      "\"Our plan is going to be this,\" says the Captain. \"There's a hot sauce bar up ahead. To the east. We ask around to find a doctor. Since
      we also need to repair the ship, let's see if we can find a mine.\"\n"
      "We all nod at this except for Mr Mail, who just keeps treading east with a low whir."
    }
    else {
      "I am on a snowy path towards civilization on the moon Enceladus. My goal from here is to go east and join up with everyone."
    }
  }

w_to OutsideTheShip
e_to SnowyPath02
}

room SnowyPath02 "Snowy Path"
{
  long_desc
  {
    if self is not visited
    {
      move Captain to location
      move Finnian to location
      move Mailx to location

      "\"... bring her back something to drink is all I'm saying, they can't just have hot sauce. Maybe a bottle of scotch to anesthetize her wound. Hey robot,
      does scotch kill the bacteria and germs around wounds?\"\n"
      "\"No,\" says Mr Mailx.\n"
      "\"What does he know, he spends his free time vandalizing encyclopedias,\" Fin says.\n"
      "\"What do you think the word anesthetize means?\" asks the Captain, who is still walking. For the first time I understand what
      Finnian has been trying to do lately, he's trying to \Bcheer her up.\b It isn't working, because he's a socially maladjusted narcissist,
      but he's trying.\n"
      "\"Ja'Rod,\" she says, \"Can you give me a scan of what is up ahead?\""

      Activate(Water_Mists)
    }
    else {

      "So, where we happen to be on Enceladus is a place that is on the opposite side of Saturn. I have heard it's a bit overwhelming when
      you're on the other side of the moon and Saturn is there looking at you like a giant angry god. The way Enceladus moves in its orbit, we
      won't ever get that view which is bad from a life experience standpoint but better from an anxiety-inducing one, I think.\n"

      "The ground is pure ice here. It isn't clear to me if humans or machines have ever truly walked on this particular part of the moon ever
      due to just how slippery it is. The path to town is to the east."
    }
  }
  before
  {
    location DoExits
    {
      "There are exits east and west from here."
    }
  }

  xRayDescription {

    if GameEvents[16] = 0
    {
      "\"I see giant spiders, tell Mr Mailx to take some on location shots for the next batch of edits,\" I say, trying to keep the conversation going. \"No,
      for real though, there is, sure enough, a bar not too far away and it's across from the entrance to an underground mine. I'm not getting
      good readings on what sort of metals are down there, so I assume it's lead and something else.\""

      GameEvents[16] = 1
    }
    else {
      "Up ahead is a bar and across the street from it is an underground mine."
    }
  }

  w_to SnowyPath01
  e_to {
    if GameEvents[16] = 0
    {
      "I need to do a scan first, as requested by the Captain."
    }
    else {
      return SnowyPath03
    }
  }
}

room SnowyPath03 "Snowy Path"
{
  long_desc
  {
    if self is not visited
    {
      move Finnian to location
      move Captain to location
      move Mailx to location

      "\"Okay, so we need a doctor to come back to the ship,\" says Finnian. \"D'ya think they'll just come because Alexandra is hurt? That would make it sort of a house call, wouldn't it? I don't know
      if doctors on this moon do house calls.\"\n"
      "\"I don't think we should bring up payment until whoever we find does, \" says the Captain. \"And if they do, let's let them throw out a number first.\"\n"
      "\"Ah-ha!\" exclains Finnian. \"So good to be on the other side of your negotiations for a chance. I see how it goes now. This mystery doc will be lucky to have an eighth share.\"\n"
      "\"None of you are at an eighth of a share,\" replies the Captain.\n"
      "\"The robot is. Hey robot, you getting an eighth of a share?\"\n"
      "\"No,\" says Mr Mailx.\n"
      "\"There ya go. Too bad you look like three poorly stacked Commodores. If the type of machine you were was a Rug Doctor the Captain might have paid you for this.\" Finnian waves his hands. \"All
      of this.\"\n"

      "I see now that we landed in just an enormous flat crater -- the crater's edge is just beyond this little section of town. I still have no idea where anyone around here lives. I'm hoping their
      homes are out of the crater and they all get around by jetpack. (There is what appears to be an abandoned snowmobile close by with tracks leading to the bar, so maybe it's just that.) To the east
      is the actual center of town."

    }
    else {
      "I'm inside the far edge of a crater here on Enceladus. The crater's walls are visible now, maybe no more than another kilometer. The whole moon is only a quarter the size of Ethiopia, where
      I grew up. A disused snowmobile lies a bit away, to the east is the hot sauce bar and the entrance to a mine."
    }
  }

  e_to CenterOfTown
  w_to SnowyPath02
}

room CenterOfTown "Center of Town"
{
  long_desc
  {
    if self is not visited
    {
      move Finnian to location
      move Captain to location
      move Mailx to location

      "There is a large log cabin to the south. Pretty rustic. Trees don't exactly grow here, so the fact that it exists at all means that someone really wanted to have an earthly experience. To the north
      is a cave, my scan indicated that people can descend downwards.\n"

      "\"I can do the talking once we g-- \" starts Finnian.\n"
      "\"I'll do the talking,\" interrupts the Captain.\n"
      "\"I'll try the milder hot sauces,\" I add, helpfully."
    }
    else {
      if GameEvents[20] = 1
      {
        move snowmobile to location
        "The Captain walks over to what I thought was a broken and useless snowmobile. She breaks open a panel underneath the steering mechanism and holds two
        wires together. Nothing happens. She rolls her eyes and gives it a push down the snowy hill leading to the hot sauce bar... only she goes straight past
        the bar and descends out of sight.\n"
        "Twenty seconds later I hear a roar.\n"
        "Ten seconds after that, I see her come back up the hill with the now-working snowmobile.\n"
        "\"Get on, let's go,\" she says to me."

        snowmobile.short_desc = ""

      }
      elseif GameEvents[19] = 1 !// Did Ja'Rod cut himself or otherwise find the doctor?
      {
        remove knife
        remove blairsUltraDeath

        if GameEvents[15] = 0 !// Did we send the Doctor, Finnian and Mailx away?
        {
          GameEvents[15] = 1
          !// This means we have sent the doctor and Finnian off.
          !// We need to have the Captain tell Finnian and N
          "The Captain sees Finnian, Mr Mailx and Doctor Nahas about to leave.\n"
          "\"You're all set?\" she asks Finnian.\n"
          "\"Yeah, we're huffing it back to the Plagoo.\"\n"
          "\"I need Mr Mailx, but I have a suspicion that Grimes programmed it to not listen to women,\" she adds. Nahas looks
          at the two of them like they are crazy.\n"
          "\"He ... Grimes told me why he did it. He didn't do it because of anything other than he didn't want his fiancee back
          on Earth to have it open up his stash of --\" I make the 'cut him off' gesture at Finnian and look towards the Doctor. Him
          getting weirded out is the last thing we need.\n"
          "\"His, ah, stash of um ... er ... women's socks,\" says Finnian, finally. The fact that it took him ten seconds to come
          up with something worse than if he just told the truth isn't helping. \"Do you need the robot, Cap? Mailx, go with the
          Captain,\" says Finnian.\n"
          "\"Okay,\" says Mailx. Finnian and the Doctor each head back to the Plagoo."

          remove Finnian
          remove MiladNahas
          move Mailx to location
          move Captain to location
        }
      }
    }
  }
  before
  {
    location DoExits
    {
      "To the south is a Hot Sauce bar. To the west is the snow path leading to the Plagoo. To the north is a mine."
    }
  }

s_to HotSauceBar
w_to SnowyPath03
n_to {
  if GameEvents[19] = 0 !// Did Ja'Rod and the Captain find a doctor?
  {
    "I can't go that way just yet. Valid exits are to the south and west."
  }
  else {
    return EntranceToMine
  }
}
}

room HotSauceBar "Hot Sauce Bar"
{
long_desc
{
  if self is not visited
  {
    PlayMusic("ENCEL01", "JAM - Your Darkest Smile", 50, true, true)
    MusicListing = 4

    move Captain to location
    Captain.talkValue = 4

    "The four of us enter the bar and we're greeted by a doorman. He is suited up in a caribou skin and fur coat. I don't think it was made for him as he's sitting on a stool and the thing
    falls all over the floor of the bar like a wedding dress.\n"
    "\"You can't bring that thing in here,\" says the bouncer, but he is pointing to Finnian. Mr Mailx turns his entire body to face Finnian, which is the creepiest thing ever because
    of how long it takes him to do that on tread.\n"
    "\"That's my navigator,\" says the Captain.\n"
    "\"There's one room and a bathroom in here, you'll be able make your way around just fine,\" says the bouncer. \"Get that cap out of here before I throw you all out myself.\"\n"
    "\"Oh, it's the cap?\" asks Finnian. \"This thing is the height of fashion far back around the outer moons of Saturn.\" The Captain rolls her eyes.\n"
    "\"This is a moon of Saturn, dummy,\" says the bouncer.\n"
    "Finnian looks genuinely surprised at that revelation. \"Huh,\" he says.\n"
    "\"Mailx, please go with Finnian and wait outside the bar,\" says the Captain.\n"
    "\"No!\" shrieks the robot.\n"
    "\"Come on, robot,\" Finnian says to Mr Mailx. The two of them leave, to presumably wait outside or try to find a print
    encyclopedia to assert the correctness of the bouncer's claim.\n"

    "Now that we're inside, this looks just like any other bar in the solar system, except colder and with a scent of chile peppers instead of old hops. There are a series of booths around the
    walls with the bar itself a square right in the middle. I'd say there's probably thirty or forty men, women and robots in here.\n"

    "The Captain points to a couple empty stools near the plastic bin of lemons and limes. \"Shall we?\" she asks."

  }
  else {
    "The bar doesn't look that much different than normal alcohol bars across the solar system. It's got a bit more rustic feel because they
    had to make it out of logs, but it adds some charm, I guess. The bar itself is in the middle of the room, and there are shelves
    here with different kinds of hot sauces, arranged by color, manufacturer, scoville rating and how many people they've made puke up.
    The lights are fairly low " ;

    if GameEvents[19] = 0
    {
      "and we don't have a lot of time to try to find a doctor."
    }
    else {
      "and I'm ready to get out of here for the most part."
    }
  }
}
  before
  {
    location DoExits
    {
      "The only exit that I can see out of here is to the north."
    }
  }

n_to {
  if GameEvents[19] = 0
  {
    "I can't leave yet, I have to help find a doctor."
  }
  else {
    return CenterOfTown
  }
}
}

room EntranceToMine "Mine Entrance"
{
long_desc
{
  if self is not visited
  {
  !// Need to mention how the Captain got the key and why she gave it to Ja'Rod
    "\"How did you get the key, anyway?\" I ask the Captain.\n"
    "\"When you were flailing about, I just explained the situation to her and promised her no more theatrics. I guess
    the mine is owned by the same company that owns the hot sauce bar. I guess they own everything around here.\"\n"
    "\"Well, they are doing a great job, they put in air, they put in a little more gravity, they put in...
    well...\" I can't really think of what else this company did to make it nice here.\n"
    "\"Yeah,\" says the Captain, out of compliments for Enceladus as well. \"I think we might be the only people
    in a decade to land here as opposed to crash land here and be stuck.\""
  }
  else {
    "The mine descends deep into the belly of the moon, but standing before us at the moment
    is a formidable door."
  }

  move Captain to location
  move Mailx to location
}

s_to {return CenterOfTown}
n_to {return MineDoor}
}

door MineDoor "door"
{
  nouns "door"
  article "the"
  between EntranceToMine, mine
  is openable, not open, lockable, locked
  key_object mineKey

  long_desc
  {
    "Looks like a reinforced metal door, about five feet tall so we'll have to duck under a little."
  }
}

room mine "Mine"
{
long_desc
{
  if self is not visited
  {

    PlayMusic("ENCEL01", "FREDERIC MOTTE - Duel", 80, false, true)
    MusicListing = 2

    move Mailx to mineLevelThree

    "\"So we need some metal for patching up the ship,\" says the Captain, just reiterating the plan. I don't quite know
    how a chunk of metal is going to help, but then it's unlikely to be my job to fix it, anyway. This area of the mine
    extends about fifteen feet north. There is a (wooden) beam overhead and a rope leading downwards.\n"
    "\"Shouldn't there be a cart or something to get us along?\" I ask.\n"
    "Mr Mailx moves slowly to the edge of the hole. \"There, Is, One, Aaaaah,\" he says, having gotten
    a little too close to the edge, and fallen. The Captain just shakes her head.\n"
    "\"Let's go get him,\" she says with a sigh."
  }
  else {
    "This is a makeshift antechamber, the best they can do here. There is snow on top of us and at all sides. The snow
    is very compact and hard. Igloo-like, maybe? We can travel downwards if we wanted to."
  }
}

s_to { return mineDoor}
d_to {
      if silverOre in player
      {
        "I'd rather not head down there again."
      }
      else {
        return mineLevelTwo
      }
    }
}

room mineLevelTwo "Mine Level Two"
{
  long_desc
  {
    if self is not visited
    {
      "The Captain and I, having descended from the rope above, come to rest on this level of the mine.\n"

    }

    "We don't really see any metal that can help us out here. It also appears that there hasn't been
    much digging, as if there was something interesting further down, but they wanted a base here in case
    of future expansion. The rope continues to lead downwards. There is some installed blue neon lighting below
    that I can see. It appears that there's just one more level to the mine."

    if self is not visited
    {
      "\nThe Captain continues by going down."
    }
  }

u_to {"We can't go that way by ourselves."}
d_to mineLevelThree
}

room mineLevelThree "Mine Level Three"
{
  long_desc
  {
    if self is not visited
    {
      move Captain to location
      "Luckily, the rope is close enough to the sides of the mine that I can sort of steady myself and kick until I descend
      completely.\n"

      "The mine shaft ends here, in terms of going into the center of Enceladus. Carved out from the rock and snow is a path
      to the east."
    }
    else {
      if GameEvents[20] = 1
      {
        move Captain to location
        move Mailx to location

        "The mine shaft ends here, in terms of going into the center of Enceladus. Carved out from the rock and snow is a path
        to the east."
      }
      else {
        "The mine shaft ends here, in terms of going into the center of Enceladus. Carved out from the rock and snow is a path
        to the east."
      }
    }
  }
  before
  {
    location DoExits
    {
      "There's an exit east of where we are now. Entering the mine cart can also, in theory, bring us up."
    }
  }

!// Mine Cart is in here and can take us up.
u_to {"We can't go that way by ourselves."}
e_to bottomOfMine
cant_go {
  "I can't go that way. " ;
  Perform(&DoExits)
  }
}

room bottomOfMine "Bottom of Mine"
{
  long_desc
  {
    if self is not visited
    {
      move Captain to location
      move Mailx to location

      Activate(Cave_In)

      "We have come to the bottom of the mine and the farthest anyone has dug. Nothing allows any further passage.\n"
      "It's a bit dark, although there is some neon rope providing a bit of light. There is icy rock on all sides of us."
    }
    else {
      "We are in the bottom of the mine here in the city. A small piece of neon rope that is powered from who-knows-what
      gives us a little bit of light, but that's it, really. It's cold in here as we are surrounded by icy rock on all sides."
    }
  }

xrayDescription {

    if iceBoulder in location
    {
      "I take a quick scan of the area. This boulder is definitely pure ice, nothing else to it as far as I can tell.
      The only issue is that the ice is harder than anything I can imagine and we have a giant chunk of it in front of us."
    }

    if GameEvents[30] = 0
    {
      GameEvents[30] = 1
      Mailx.talkValue = 2 !// "I should talk to the Captain first."
      Captain.talkValue = 1

      move silverOre to location

      "\nI... okay, I do find something interesting in the mine itself, however, a vein of silver ore about the size of a baseball, just
      half an inch from the side of the northern wall. Covered in much more supple icy snow, I dig at it until it almost pops
      out and ends up on the ground. I pick it up. I have no idea if the Captain is aware of what I just did, I should definitely talk
      to her about it."

      move silverOre to player
    }

    if iceBoulder not in location and GameEvents[30] = 1
    {
      "I don't get anything interesting."
    }
  }

before
{
  location DoExits
  {
    if GameEvents[20] = 0
    {
      if iceBoulder in location
      {
        "We're stuck here unless we can figure out a way to get past the boulder."
      }
      else {
        "There is an exit west."
      }
    }
    else {
      "There is an exit west."
    }
  }
}

w_to {
  if GameEvents[20] = 0
  {
    if iceBoulder not in location
    {
      "I can't go that way at the moment, I don't want to leave the Captain."
    }
    else {
      "We can't go that way due to the cave-in."
    }
  }
  else {
    return mineLevelThree
  }
}
}

room RecRoom "Rec Room"
{
  long_desc
  {
    if self is not visited
    {
      PlayMusic("ENCEL01", "FREDERIC MOTTE - Knulla Kuk", 80, true, true)
      MusicListing = 3

      "The Recreation Room of the Plagoo is twenty feet by fifteen feet and placed way in the 'back' of the ship. I think the theory
      for the designer of the class of ship that the Plagoo is, is that we'd constantly have people firing weapons at us, so
      put the least important thing towards the tail. We are all gathered here. I am the last to arrive and overhear the
      Captain talk about how we can process the aluminum until we can get to a shipyard with Finnian while asking Doctor Nahas
      to help with arrangements for Grimes's body.\n"

      "The Captain sees that I have arrived and gives me a genuinely warm smile, the first I've seen her make in ages. \"I think
      we just need to all have a drink together after everything that went down,\" she says to all of us.\n"

      "The Captain and Finnian each have a glass of scotch. Doctor Nahas has a ginger ale. Alexandra has an entire
      bottle of vodka and someone gave Mr Mailx a Dixie Cup with engine oil in it. (He can't actually drink anything, so
      this was someone's attempt at humanizing him.) Alexandra hands me a drink of my own invention, a gintata.\n"

      "\"To those we miss terribly and to all who remain,\" says the Captain as everyone raises their glass."

      Captain is hidden
      MiladNahas is hidden
      Finnian is hidden
      Mailx is hidden
      Alexandra is hidden

    }
    else {
      "This is the rec room of the Plagoo. Everyone here has their glasses raised in a toast, with drinking of said
      beverage no doubt the next thing everyone does.\n"

      "My friends are here."
    }
  }
}
