!routines.hug

routine DoAbout()
{
	"Enceladus by Robb Sherwin."
	"Originally submitted for the 2019 Interactive Fiction Competition."
	"You can talk to people just by using the >talk command, it's context sensitive."
	"You can see current exits for the room you're in by doing >exits"
	"Try >help for version info, credits and other common commands."
	"Please use the Hugor interpreter for Windows, Mac and Linux: http://ifwiki.org/index.php/Hugor"
	"To play other games by this author, go to https://www.joltcountry.com/games"
	"For Frobozz."
}

routine DoDrive()
{
	"I can't drive that."
}

routine DoExits
{
	YouCanGo()
}

replace DoKiss
{
	"This is an awful time for that."
}

routine DoLower
{
	"That can't be lowered."
}

routine DoRaise
{
	"That can't be raised, unfortunately."
}

routine DoRead
{
	"That doesn't seem to be readable."
}

routine DoRespond
{
	"There's no computer here to respond to an instant message with. (Try using the talk command, like talk to Captain)"
}

routine DoScan
{
	if xRaySpecs is special
	{
		if location.xRayDescription
		{
			!// The check up there runs the xRayDescription
			Activate(SpecsGoUp)
			return true

		}
		else {
			"Nothing special to report here."
		}
	}
	else {
		if xRaySpecs in player
		{
			"My specs aren't lowered."
		}
		else {
			"I need my specs for that."
		}
	}
}

routine DoShoot
{
	"That can't be shot!"
}

routine DoTwirl
{
	"I can't twirl that now, come on!"
}

routine Final_Scene()
{
	"I take a sip of my gin-tata. The Kool-Aid tastes pretty fresh, I have no idea where you get Fruit Punch flavor this far from Earth. The Captain consumes her
	scotch without fanfare, Finnian starts in on his with as much fanfare as humanly possible. Alexandra opens the cap slightly and with a flick of her finger
	sends it spinning clear across the room. She takes a long pull from bottle of vodka and Doctor Nahas looks at all of us, leaving his drink raised."

	"\n[Strike any key to continue]"

	pause

	"\nAlexandra keeps drinking the vodka."

	"\n[Strike any key to continue]"

	pause

	"\nAlexandra keeps drinking the vodka. The oil melts through the Dixie cup Mailx was holding, creating a mess. Doctor Nahas drinks his ginger ale out of
	polite awkwardness that Alexandra hasn't stopped yet."

	"\n[Strike any key to continue]"

	pause

	"\nAlexandra keeps drinking."

	"\n[Strike any key to continue]"

	pause

	"\nAlexandra finishes the bottle of vodka. Everyone except Mailx is looking in her direction. Maybe he is too. \"I'm parched,\" she says. \"A girl really feels
	the lack of humidity out here on the Jovian moons,\" she says as Finnian's eyes light up and he gestures in laughter towards the Captain. I look around
	for something to clean the spill up with as I make myself another drink. We might be a million miles from where we meant to be and ten
	million miles from where we want to be but at least for the moment we're right where we belong. Together."

	"\n[Strike any key to continue]"

	pause

	endflag = 1

}

routine MoveEveryoneToRecRoom()
{
	move Captain to RecRoom
	move Alexandra to RecRoom
	move Mailx to RecRoom
	move MiladNahas to RecRoom
	move Finnian to RecRoom

	move refreshingBeverage to player

	!// Move the other drinks to the other characters

	MovePlayer(RecRoom)
}

routine PrintRandomCrowdDiscussion
{
	local randomNum

	randomNum = random(6)

	select(randomNum)
	case 1
	{
		"\"... I don't care how bad it is for the Earth, we live a mile up. In fact, I'm going to be throwing my Spray Pam Directly At The Sun Party. You're invited. You don't have to bring
		anything. Except about forty cans of Pam.\""
	}
	case 2
	{
		"\"... The commute is between Phobos and Deimos and takes forever. And where I park on Phobos is near a bunch of bugs. They don't come out until I'm halfway to Deimos and that's why I'd like
		you to help me out. I'm like a Lyft driver for winged bugs. What? I don't kill them, no. Of course when I land I let them go. I said Lyft driver, not Uber.\""
	}
	case 3
	{
		"\"... I don't want to talk about my ex, they say to never do that on a first date. If you insist... ok. He had the Centaur procedure done. Yeah. After we - yeah. Year and a half of dating,
		I come home to that. You know what's hot once and exactly once? The second Centaur - right. Right. So. What do you do for fun? Please let it not be Dungeons and Dragons.\""
	}
	case 4
	{
		"\"... I need a driver. Hmm? Yes, from a Neptunian moon to the Moon. Yes, driving around on the surface as well. What do you mean that's two jobs? What? Driving on the surface of Triton is
		a separate job from going to Triton to the Moon? Are you trying to screw me? It's driving! Ok. Ok. Ok. Still driving!\""
	}
	case 5
	{
		"\"... Yeah, I got tons of it. Not literally tons, it's smaller than Earth. It fell out of the sky. I told you you wouldn't believe me.\""
	}
	case else
	{
		"\"... Protocols changed, if you were still using the old way to encrypt your phone you might as well have been hitting the play button while the bad guys were in the room. Well, I
		can never remember if it's apt-get or the other way so I didn't personally upgrade, no. I don't have enough money for anyone to steal. Which reminds me. Thanks for the sauce.\""
	}
}

routine StartPlanetFallScene
{
	MovePlayer(OutsideTheShip)
}

replace DoWakeCharacter
{
	"That's probably not necessary."
}

routine VersionOfGame()
{
	print "\BEnceladus\b"
	print "Interactive Fiction"
	print "Copyright (c) 2019. Written and programmed by Robb Sherwin"
	print "Release 1.00 / Serial number 190123 / Hugo 3.1"
	print "Please type >about for specific information and >help for Help."
	""
}
