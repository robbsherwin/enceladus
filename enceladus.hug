!\---------------------------------------------------------------------------

  ENCELADUS
  by Robb Sherwin
	Started: 10/16/2011
	Re-started: 6/27/2019

---------------------------------------------------------------------------\!

! Uncomment the following to use a precompiled version of the Hugo Library:
! #set PRECOMPILED_LIBRARY

! Uncomment the following to include the HugoFix Debugging Library:
! #set DEBUG

! Uncomment the following to include verb stub routines:
#set VERBSTUBS
#set USE_VEHICLES
#set NEW_DESCRIBEPLACE

#ifset DEBUG
#switches -d
#endif

#set AT_HOME
constant	 RELEASE "1.0"
constant	 NUMCHARACTERS 20
constant	 NUMENEMYNPCS 2
constant	 MAXGAMEEVENTS 200
constant	 MAXSONGS 5

!// Verbs
verb "about"
  * DoAbout

verb "exits"
  * DoExits

verb "lower"
  * DoVague
  * "the"/"my" object DoLower
  * object DoLower

verb "raise"
  * DoVague
  * "the"/"my" object DoRaise
  * object DoRaise

verb "scan"
  * DoScan
  * "room"/"location"/"area" DoScan

verb "shoot"
  * DoVague
  * object DoShoot

#include "roodylib.g"
#include "verblib.g"			! grammar must come first
#include "hugolib.h"
#include "roodylib.h"
#include "system.h"
#include "window.h"
#include "resource.h"
#include "variables.hug"
#include "robblibrary.h"
#include "routines.hug"
#include "daemons.hug"
#include "charexclaim.hug"
#include "glk.h"
#include "introscreen.hug"
#include "objects.hug"
#include "music.hug"
#include "cango.h"

global GRAPHICS_ON = TRUE
global last_drawn_picture
global force_repaint
global current_resource_file
global current_resource
global old_screenwidth
property graphic

global testValue = 0 !// Just used when we need a spare use-once variable
global mistingValue = 30

global	music_volume = 80

array temparray[10] = 0,0,0,0,0,0,0,0,0,0
array TempTimeArray[5] = 0,0,0,0,0
array LastTimeArray[5] = 0,0,0,0,0
array MusicArray[MAXSONGS]
array MusicName[MAXSONGS]
array MusicArrayTime[MAXSONGS]
array randomArgs[7]

array GameEvents[MAXGAMEEVENTS]
! GameEvents[1] = Has Ja'Rod given the jumpsuit to the Captain?
! GameEvents[2] = Has Grimes been mentioned yet?
! GameEvents[3] = Can the HallwayDoor be opened?
! GameEvents[4] = Was the bed looked at? This is for a later joke regarding shooting the hull
! GameEvents[5] = Did Ja'Rod get the star pistol from Finnian?
! GameEvents[6] = Stops flavor text from being printed twice in Hallway03
! GameEvents[7] = Did Ja'Rod look after lowering his specs?
! GameEvents[8] = Did the Captain praise Ja'Rod for looking into the next room?
! GameEvents[9] = Does the Captain want us to shoot the pistol?
! GameEvents[10] = Did Ja'Rod shoot the pistol at the hull successfully?
! GameEvents[11] = Is this our second time in the Before the Airlock room?
! GameEvents[12] = Can Ja'Rod leave the Helm?
! GameEvents[13] = Has Mailx been pulled out of the hole in the hull? - also use a flag, mailxIsPulled which is set to false initially
! GameEvents[14] = Can we leave BeforeTheAirlock and go back towards the Bridge, in other words, is alexandraInAirlock = true and is mailxIsPulled = true?
! GameEvents[15] = Did Ja'Rod cut himself?
! GameEvents[16] = Has Ja'Rod done a scan on the SnowyPath02?
! GameEvents[17] = Has Ja'Rod initially sat on the stool?
! GameEvents[18] = Is Ja'Rod incapacitated via hot sauce?
! GameEvents[19] = Did Ja'Rod and the Captain find a doctor?
! GameEvents[20] = Is the cave-in solved?

global alexandraInAirlock = false
global mailxIsPulled = false

routine init
{
  FORMAT = FORMAT | DESCFORM_F | NOINDENT_F

	InitializeMusic()

	display.title_caption = "ENCELADUS by Robb Sherwin"

	counter = -1

	glkcheck()

	STATUSTYPE = 0                  ! score/turns

	TEXTCOLOR = DEF_FOREGROUND
	BGCOLOR = DEF_BACKGROUND
	SL_TEXTCOLOR = DEF_SL_FOREGROUND
	SL_BGCOLOR = DEF_SL_BACKGROUND

	prompt = ">"

	NewMenu()

	cls

	DEFAULT_FONT	= PROP_ON
	Font(DEFAULT_FONT)

	player = JaRod                    ! player initialization
	location = TheCaptainsQuarters
	old_location = location

	move player to location
	FindLight(location)

	locate 1,10

	DescribePlace(location)
	location is visited
	CalculateHolding(player)

	Activate(CaptainIdle)

  MovePlayer(mineLevelThree)
  !MovePlayer(TheCaptainsQuarters)

	old_screenwidth = display.screenwidth

#ifset USE_PLURAL_OBJECTS
	InitPluralObjects
#endif
}

#ifset PRECOMPILED_LIBRARY
replace main
{
#else
routine main
{
#endif
	counter = counter + 1
	PrintStatusLine
	run location.each_turn

	!MusicManager()
  ! 20190811 - turning this off because I want to do scene based music.

  runevents
	RunScripts
	if parent(speaking)~=location
		speaking = 0

}


character NPC
{
type NPC

is living

maxhitpoints 10
hitpoints 10
armorclass 10
attackadjust 0
damagepotential 1
peaceful true

	before
	{
		object DoLook
		{
		!UpdateFaceWindow(self)

		run self.long_desc
		if (self.conscious = 0) and (self is not female) and (self.hitpoints <= 0) and (self.hitpoints > -100)
		{
			"\n";
			CPronoun(self)
			"is currently ";

			! Would have to do this again below if any female characters can be drunk
			if self is drunk
			{
				"passed ";
			}
			else
			{
				"knocked ";
			}

			"out."
		}

		if (self.conscious = 0) and (self is not female) and (self.hitpoints <= -100)
		{
			"\n";
			CPronoun(self)
			"is dead."
		}

		if (self.conscious = 0) and (self is female) and (self.hitpoints <= 0) and (self.hitpoints > -100)
			"\nShe is currently knocked out."

		if (self.conscious = 0) and (self is female) and (self.hitpoints <= -100)
			"\nShe is dead."

		}
		object DoWakeCharacter
		{
			if self.conscious = 1
			{
				"That's not necessary."
			}
			else
			{
				"You try, but you don't get any sort of response back into consciousness."
			}
		}

		object DoKiss
		{
			"That's not appropriate."
		}
		object DoListen
		{
			"Nothing out of the ordinary."
		}

	}

in_scope
	{ if self in location : return 1
          else : return 0
        }

conscious
	{ if self.hitpoints > 0 : return 1
	  else : return 0
	}

}


player_character JaRod "I"
{
pronoun "I" "me" "my" "myself"
nouns "jarod" "me" "self" "ja'rod" "butler"
adjectives "ja'rod" "jarod"

	long_desc
	{
		"I am a twenty-three Ethiopian male, I am 5'9\" tall and I weigh 115 pounds. I have black hair and brown eyes. I am wearing a white T-shirt, blue denim jeans and black Doc Martins for shoes. My
    hair is buzzed pretty tightly against my head and I am clean-shaven."
	}
}

room TheCaptainsQuarters "The Captain's Quarters, on the HMCS Plagoo"
{
extra_scenery "wall" "walls"

	long_desc
	{
		if self is not visited
		{
			DoInitialGameRoutines()

			"\"Look, Captain, there's a werewolf loose on the ship. We have to go - now!!\"\n"
			"\"Who are you?' she says. \"And where are my pants??\"\n"
			"Ah, cripes!\n"

			VersionOfGame()

			"I'm in the Captain's Quarters of the HMCS Plagoo. She has the biggest room not because
			it's her ship, but because we all figured out pretty early that she would be the only one
			who would clean it. She has a dresser and computer and, along the starboard wall, her bed."

      Captain.talkValue = 2 
		}
		else
		{
			"The big thing with the Captain's living quarters is that it currently doesn't contain a werewolf trying to gorge
			itself on any of the crew. That's fairly clutch. The Captain is wearing a long T-shirt, having just been roused out
			of bed because it's the middle of the night.\n"

			"Her quarters contains a dresser, a computer and (along the starboard wall) her bed.\n"

			"(We're in a space ship ";
			OneOf("cruising","flying","headed")
			" toward Saturn.)\n"

			"There is a door portside to a long hallway."
		}
	}

w_to {return HallwayDoor }

}


door HallwayDoor "hallway door"
{
nouns "door"
article "the"
is locked, lockable, not open, openable
between Hallway02 TheCaptainsQuarters
key_object fakeKey

	long_desc
	{
		"It slides into the wall with a light touch."
	}
	before
	{
		object DoTouch
		{
			Perform(&DoOpen, self)
		}
		object DoOpen
		{
			select(GameEvents[3])
			case 0
			{
				"I can't -- there is a werewolf loose on the ship!"
			}
			case 1
			{
				GameEvents[3] = 2

				"I open the door, but nothing jumps out at us. I can't explain the following feeling, but I
				am compelled to state the obvious anyway.\n"

				"\"Nothing out there, \" I say. \"But it's dark, and -- \"\n"

				"\"No, look down, kid,\" says Finnian. \"Grimes...\" Grimes, the geographer we picked up just this week and added to the crew.
				His body has been ripped apart and slung ten meters down the darkened hall. He weighed at least three hundred pounds, which leaves
				a lot of biomass to be slung about the floor.\n"

				self is open
				self is not locked
        move Grimes to location

				Activate(DiscussGrimesDeath)

			}
			case else
			{
				"I don't need to worry about it any more."
			}
		}
	}
}

resource "ENCEL01"
{
	"jpg/titlescreen01.jpg"
	"mod/BIONIC - Pure Motion.mod"
	"mod/JESTER - Elysium.mod"
	"mod/JESTER - Wizardry.mod"
  "mod/JESTER - Stardust Memories.mod"
	"mod/JOGEIR LILJEDAHL - G-Comp.mod"
	!".\mod\HELLSUN - We Have a Trance.xm"
  !"mod/JESTER - Disco Dreaming.mod"
  "mod/Yomaru Kasuga - Blue Danube.mod" !// Original song was yk_-_bluedanube.mod

}

!// CC Licenses
!// Elysium - https://modarchive.org/index.php?request=view_by_moduleid&query=40475 Attribution-NonCommercial-ShareAlike 3.0 Unported (CC BY-NC-SA 3.0)

#include "characters.hug"
#include "locations.hug"
