object captainComputer "computer"
{
nouns "computer" "monitor" "screen" "pc" "board"
adjective "computer" "pc" "circuit"
article "the Captain's"
is hidden, switchable
in captainDesk

	long_desc
	{
		"The computers on the HMCS Plagoo are extremely old, as it's all the Captain could afford after she bought the ship.  This one is a small circuit board with connectors
		jutting out. ";

		if instantMessage01 is not known
		{
			"The Captain has an unread instant message."
		}

		if instantMessage01 is special
		{
			"\nThere's a message from my fellow crew member Finnian blinking on the screen."
		}
	}
	before
	{
		object DoGet
		{
			"That's too heavy and bulky for me to take!"
		}
		object DoSwitchOn, DoSwitchOff
		{
			"It's self-booting and just stays on all the time."
		}
	}
}

scenery lightSwitch "light switch"
{
	nouns "switch"
	adjective "light" "lights"
	article "the"
	is switchable, switchedon
	in TheCaptainsQuarters

	long_desc
	{
		"I can't find it!"
	}
	before
	{
		object DoSwitchOn, DoSwitchOff
		{
			"I don't know where it is!"
		}
	}
}


object instantMessage01 "instant message"
{
nouns "message" "instant" "im"
adjective "instant" "unread"
article "the"
is hidden, readable, special
in TheCaptainsQuarters

	long_desc
	{
		instantMessage01 is not special

		"It's from Finnian O'Connell, the ship's pilot. I have to steady myself before reading it: in order to protest the temporary pay cut the
		Captain enforced until things turn around, Finnian changed his instant message font to Comic Sans.\n"

		"\B\Ifoconnell \i:\b there's  a were wolf on the ship"
		"\B\Ifoconnell \i:\b hello?"
		"\B\Ifoconnell \i:\b cap, r u there?"
		"\B\Ifoconnell \i:\b i woulda thouhgt that would have gotten more of a reaction tbqh"
		"\B\Ifoconnell \i:\b ok ill come over"
	}
	before
	{
		object DoGet, DoMove
		{
			"Instant messages aren't something people can take with them. Most of them aren't even remotely in anything that approaches English!"
		}
		object DoRead
		{
			Perform(&DoRead, self)
		}
	}
}


object captainDesk "desk"
{
nouns "desk"
article "the"
in TheCaptainsQuarters
is hidden, platform

	long_desc
	{
		"It's made of a lightweight plastic and painted a cheap, glossy silver."
		WhatsIn(self)
	}
	before
	{
		object DoGet
		{
			"I can't take the desk, unfortunately(?)."
		}
	}
}


scenery captainDresser "dresser"
{
nouns "dresser"
article "the"
in TheCaptainsQuarters
is hidden, container, openable, not open
parse_rank -1

	long_desc
	{
		"The Captain's dresser is a five foot-tall light aluminum storage container that she keeps her clothes in. It has a small
		dent in one side where it has been struck repeatedly."
	}
	before
	{
		object DoHit
		{
			Perform(&DoOpen, self)
		}
		object DoGet
		{
			"I can't take the dresser, unfortunately(?)."
		}
	}
}

object captainBed "bed"
{
nouns "bed"
article "the Captain's"
is enterable, hidden
in TheCaptainsQuarters

	long_desc
	{
		"She somehow fit a king-size in here. None of us have any idea how she got it past the door. My impression was that she cut away part
		of the hull. It's pretty old, but mattresses last longer when it's a single female using them. The rest of us got rack upgrades from a
		RestWell (TM) vendor on Mimas last year."

		GameEvents[4] = 1
	}
	before
	{
		object DoEnter
		{
			"That's a bit presumptuous."
		}
		object DoGet
		{
			"I couldn't have gotten it in here, I certainly can't get it out!"
		}
	}
}

scenery bedCovers "covers"
{
	nouns "covers"
	article "the bed's"
	in TheCaptainsQuarters

	long_desc
	{
		"The covers are on top of the bed, you can hardly tell that the Captain slept at all in them. They have a marble pattern, and are officially
		called a duvet cover set."
	}
}


object jumpsuit "jumpsuit"
{
nouns "suit" "jumpsuit" "pants"
pronoun "it"
adjectives "jump" "clothes"
article "the Captain's"
in captainDresser
is clothing

	long_desc
	{
		"So, it's a size 4 (US) single piece, bandeau styled for a size-conscious fit. It's got glitter on it, but I don't know if
		the Captain added it herself or what."
	}
	before
	{
		object DoGet
		{
			if jumpsuit in Captain
			{
				"I can't do that."
			}
			else {
				move jumpsuit to player
				"I take it out of the Captain's dresser."
				object = jumpsuit
				xobject = jumpsuit
			}

		}
		object DoWear
		{
			"It would never fit m-- ... it would probably fit me, but I'm still not going to wear it!\n"
			"I should probably just give it to the Captain."
		}
		object DoGive
		{
			select xobject
			case Captain
			{
				GameEvents[1] = 1
				move tshirt to captainBed
				tshirt is not hidden
				move jumpsuit to Captain
				jumpsuit is hidden
				move latte to Captain

				"The Captain throws off her long t-shirt and shimmies into her jumpsuit. She produces a latte out of nowhere. (I mean, I
				wasn't looking when she got changed and everything. I am the only gentleman on this ship. I have a reputation, narmean?)\n"

				Pause100thSeconds(100)

				move latte to Captain

				"\"I'm ready to communicate with you now, Ja'Rod,\" she says, at last smiling.\n"

				"\"About the werewolf -- \"\n"

				"\"Right.\" She sips from the latte. She seems much more alert. \"With you now.\" She doesn't slurp her latte. She and the crystal 'Mouse Trap' game
				in the rec room are the only classy parts of this ship.\n"

				"\"I don't know how it got on, but -- \"\n"

				Pause100thSeconds(400)

				"Someone or some thing enters a passcode to the door from outside her quarters. " ;

					if HallwayDoor is not locked
					{
						"In my horror I realize that \Inobody has locked the door.\i"
					}
					else {
						""
					}

				Activate(FinnianEnters)

			}
			case Finnian
			{
				"I'm sure he'd jump at the chance to get extra attention by wearing it, but it's meant for the Captain. I think
				it's the only way she's gonna focus at this hour."
			}
			case else
			{
				"It's the Captain's!"
			}
		}
	}
}

object latte "latte"
{
nouns "coffee" "latte"
adjectives "drink" "angelfish" "french"
article "the"

	long_desc
	{
		"I think the Captain prefers her latte from the Angelfish brewing company straight from beautiful
		Paris. She brought a bunch of bags with her for this trip."
	}
	before
	{
		object DoDrink
		{
			"It belongs to the Captain, though -- she's very particular about her coffee!"
		}
		object DoGet
		{
			"It's the Captain's! Taking latte from the captain of a ship is a punishable offense. By my captain, anyway."
		}
	}
}


object tshirt "long t-shirt"
{
nouns "shirt" "tee" "t-shirt"
adjectives "tee" "long"
article "the Captain's"
is clothing, hidden
in Captain

	long_desc
	{
		"I've never seen her in it before. Grimes ";
		if GameEvents[2] = 0
		{
			"(this other crewmember) ";
		}
		"said he caught her raiding the fridge one night, but Grimes sort of camped there a lot.
		He said it was from someone she cared about a long time ago. That made it definitely not from Grimes."
	}
	before
	{
		object DoGet
		{
			"It doesn't belong to me and can't help with the werewolf problem."
		}
	}
}

object xraySpecs "x-ray specs"
{
nouns "specs" "x-ray"
adjectives "x-ray"
article "the" "my"
!// Not special is up, special is down
is clothing, hidden, not special, plural
in JaRod

	long_desc
	{
		"The specs are a set of chromatic filters that allow me to see the world in an x-ray sense - it helps a little bit in seeing behind objects
		unless those objects are particularly dense. I can \Bscan\b things when they are lowered.\n"
		"The x-ray specs are currently " ;
		if self is special
		{
			"lowered."
		}
		else {
			"raised."
		}
	}
	before
	{
		object DoDrop
		{
			"They are essential for me to do my job, I shouldn't be without them."
		}
		object DoLower
		{
			if self is special
			{
				"They are already lowered!"
			}
			else {
				self is special
				"Okay, I've lowered my x-ray specs."
			}
		}
		object DoRaise
		{
			if self is not special
			{
				"They are already raised."
			}
			else {
				self is not special
				"My x-ray specs are now raised."
			}
		}
	}
}

object starPistol "star pistol"
{
nouns "pistol" "gun" "weapon"
adjective "star"
article "the"

	long_desc
	{
		"There was a wave of retro nostalgia that hit laser weapons about a decade ago. This pistol looks like a atom-for-atom
		reconstruction of a French Navy pistol, with a curved walnut stock, brass fittings. Somewhere inside are the electronic
		components allowing it to produce steel-melting laser beams."
	}
	before
	{
		object DoDrop
		{
			"I've been tasked with holding onto this, so I probably shouldn't just leave it around."
		}
		object DoTwirl
		{
			"I twirl the star pistol around my index finger like a cowboy! Haha, I'm a space cowboy!"

			if GameEvents[25] = 0 and Captain in location
			{
				GameEvents[25] = 1
				"\n\"Oh my, be careful with that, please, Ja'Rod,\" says the Captain."
			}
		}
	}
}

scenery airlock "airlock"
{
	nouns "airlock"
	article "the"
	!No initial location, we will move this in later.

		long_desc
		{
			if greenButton not in location
			{
				"The airlock allows us to get things in and off the ship without exposing the entirety of the ship to the cold vacuum of space. Right
				now there isn't anyone inside. The door to the outside is closed but the door closest to us is open. Mr Mailx and Alexandra are closest
				to the airlock itself, with the werewolf and McGrathley near the control panel."
			}
			else {
				if greenButton is not special
				{
					"The transparent wall of the airlock, the one closest to me, is down. " ;
				}
				else {
					"The transparent wall of the airlock, the one closest to me, is up. " ;
				}

				if redButton is not special
				{
					"The door to the outside is closed. " ;

				}
				else {
					"The door to the outside is closed. " ;
				}

				if alexandraInAirlock = true
				{
					"Alexandra is inside the airlock right now, which is good - we want her in there so we can control the temperature of that space and keep
					her warm without having to move her too much."
				}
				else {
					""
				}
			}
		}
}

scenery airlockHull "hull"
{
	nouns "hull"
	article "the"
	in BeforeTheAirlock

	long_desc
	{
		if GameEvents[10] = 0
		{
			"The hull here is interesting because there are two people in front of it that are not supposed to be on our ship."
		}
		else {
			"I have blasted the hull on the Captain's orders. Mr Mailx, our ship's robot, is currently sealing the hull with its
			behind exposed to the vacuum of space, plugging it."
		}
	}
	before
	{
		object DoShoot
		{
			if GameEvents[10] = 0
			{
				GameEvents[10] = 1

				"I emerge from behind some crates and take aim at the section of the hull behind McGrathley and the werewolf. I am shaking
				a bit, but luckily you don't actually have to be that accurate when blowing a hole in your ship. The star pistol takes a second
				 to warm up after I squeeze the trigger. A ton of ping there. The werewolf probably had enough time to jump
				over the crates and rip out my neck, but I think it was about to bring up the fact that McGrathley got a spacesuit
				for himself and not it. Regardless - oh, there it goes.\n"
				"There is a horrible screeching sound as the air and everything else nearby starts to get sucked out by the small hole.\n"
				"The werewolf never had a chance. Being closest, it gets fired through and sent flying into the nothingness of space. McGrathley
				maybe had an extra half-second, but he was next. He definitely lowered his helmet before he was ejected.\n"
				"\"Don't body shame me!\" shouts Finnian towards our ejected interlopers while all this is going on.\n"
				"I then comprehend the Captain's plan - Mr Mailx was next, but was too large for the pistol's hole. He instead plugged
				the hole like a Dutch boy's finger. It was all over before Alexandra, clearly injured, even really moved.\n"
				"\"Finnian, Ja'Rod,\" starts the Captain, \"Get over to the bridge, we're going to perform an emergency landing.\"\n"
				"\"Aye-aye,\" I say and get going. Fin does the same."

				"\n[Strike any key to continue]"

				pause

				MovePlayer(Bridge)
				move Captain to Bridge
				move Finnian to Bridge
				remove werewolf
				remove McGrathley
			}
			else {
				"Well, I can't shoot it again."
			}
		}
	}
}

scenery greenButton "green button"
{
nouns "button" "green"
adjective "green"
article "the"
! Not going to have them in the airlock initially, since Ja'Rod can't get to them
is not special

	long_desc
	{
		"The green button controls the transparent door on the 'ship' side of the airlock. " ;
		if self is not special
		{
			"The door is currently closed."
		}
		else {
			"The door is currently open."
		}
	}
	before
	{
		object DoPush
		{
			if self is not special
			{
				self is special
				"The first door to the airlock is now open."

				if alexandraInAirlock = false
				{
					alexandraInAirlock = true
					{
						"\nAlexandra, still very much holding what I think is her lower intestine, crawls over inside the airlock."
					}
				}
			}
			else {
				self is not special
				"The first door to the airlock is now closed."

				if alexandraInAirlock = true
				{
					"\nAlexandra nods at me."
				}
			}
		}
	}
}

scenery redButton "red button"
{
nouns "button" "red"
adjective "red"
article "the"
! Not going to have them in the airlock initially, since Ja'Rod can't get to them
is special !// Going to make it open for starters and special means OPEN

	long_desc
	{
		"The red button controls the opaque door on the 'empty vacuum of space' side of the airlock. " ;
		if self is not special
		{
			"It is currently closed."
		}
		else {
			"It is currently open."
		}
	}
	before
	{
		object DoPush
		{
			if self is not special
			{
				self is special
				"The door to the outside is now open."

				if GameEvents[14] = 1 !// Meaning, Alexandra is in the airlock
				{
					"\nAlexandra looks at the open door and then looks at me.\n"

					if greenButton is not special
					{
						"She mouths something that I can't hear because the first door is closed.\n"
						"\"She. Is. Saying. Close. The. Outside. Door. Dummy,\" offers Mailx."
					}
					else {
						"\"Close the door, dummy! Red button! It's freezing in here!\""
					}
				}
			}
			else {
				self is not special
				"The door to the outside is now closed."
			}
		}
	}
}

scenery blanket "blanket"
{
	noun "blanket"
	article "the"

	long_desc
	{
		"It's not really all that important, but I think it's a carbon fiber and silk mesh Snuggie."
	}
}

scenery navigationControls "navigation controls"
{
	noun "controls" "console"
	adjective "navigation"
	article "the"
	in Bridge

	long_desc
	{
		"The controls are in front of Finnian, they are what he uses to move the Plagoo - to apply thrust, yaw, roll,
		tilt, all those things I did playing computer games as a kid that I would never dare try in real life. The
		console is all buttons, though, the Captain couldn't afford the flightstick option for the Plagoo."
	}
}

scenery captainsChair "Captain's chair"
{
	noun "chair"
	article "The Captain's"
	in Bridge

	long_desc
	{
		"The Captain did splurge a little on the chair, the Plagoo didn't originally come with it. It has a giant leather
		rectangle that forms the back and a couple more for upper back support and a seat. There are a couple of wooden
		arm rests as well, and two touchscreens beyond those."
	}
}

scenery touchscreens "touchscreens"
{
	noun "screens" "touchscreens" "touchscreen" "screen"
	article "the"
	in Bridge

	long_desc
	{
		"They aren't showing anything and don't belong to me."
	}
}

scenery scanningStation "Scanning Station"
{
	noun "station" "console"
	adjective "scanning"
	article "the"
	in Bridge

	long_desc
	{
		"This is the area that I do my work in when I am on the Bridge. My job entails scanning, er, things and getting information.
		So I've got my X-ray specs when things are up close but there's also a lot of scans I run at this console. Typically
		I try to get an analysis of other ships when we encounter them but I also provide local weather when we are landing
		on a planet or, past the asteroid belt, a moon."
	}
}

object barStool "bar stool"
{
	noun "stool"
	adjective "bar"
	article "the"
	is hidden
	in HotSauceBar

	long_desc
	{
		if GameEvents[27] = 1
		{
			"It's a little tall. Look, I'm above average in height. Okay? And this thing is just making a production of the fact that my feet don't touch the floor and the Captain's don't. Review: negative!!"
		}
		else {
			"Thing's a bit tall, isn't it? Look, I'm above average in height. Depending on the source. Okay? And this thing is menacing over me like Carbonite Han in Jabba's Palace. I'd best check it for strings."
		}
	}
	before
	{
		object DoSit
		{
			"I take a seat on the bar stool." ;

			if GameEvents[27] = 0
			{
				GameEvents[27] = 1
				" The Captain joins me to my left.\n"
				"The bartender is a late-twenties Asian gal, I'd place her right in the middle of Los Angeles, California with her horn-rimmed glasses and all business body language.\n"
				"\"Right, fancy a mite o Frank's eh come noe lurves?\"\n"
				"(Nailed it, deep southeast L.A. near Baker, I think.)"
			}
		}
		object DoGet
		{
			"Even though the gravity here is lower, my desire to carry around a bar stool doesn't become higher."
		}
	}
}

scenery crowd "crowd"
{
	noun "crowd" "patrons" "people" "drunks"
	article "the"

	long_desc
	{
		"They are all huddled together for warmth and conversation. There is low security out here on the moons of Saturn and no videotaping or audio recording, so people inevitably
		flock here to discuss illicit deals and the like."
	}
	before
	{
		object DoListen
		{
			PrintRandomCrowdDiscussion()
		}
		object DoTalk
		{
			"I would feel weird just going up to them and starting to talk to them! I don't mind listening to them, but the thought of directly speaking to them is too much for me."
		}
	}
}


object fruitTray "fruit tray"
{
	nouns "tray"
	adjective "fruit"
	article "the"
	in HotSauceBar
	is openable, not open

	short_desc
	{
		"\nOn the bar is a small fruit tray."
	}

	long_desc
	{
		"I am not sure what the proper name for this thing is, it's probably not fruit tray, but that's what I am going to go with, I guess. "
	}
	before
	{
		object DoGet
		{
			"\"Hey! Put that down!\" exclaims the bouncer from the entrance. I make a gesture by raising both of my hands to show him that I am not
			armed, which seems appropriate to me and I assume nobody else."
		}
	}
}

object knife "knife"
{
	nouns "knife"
	article "the"
	in fruitTray

	long_desc
	{
		"It's inexplicably a cheap steak knife that is being used to slice up lemons and limes here. I suspect its main use, however, is in
		cutting the plastic wrap off hot sauce bottles."
	}
	before
	{
		object DoGet
		{
			if GameEvents[27] = 0
			{
				"I should sit on the stool and get closer to the bar first."
			}
			else {
				"Taken."
				move knife to player
			}
		}
		xobject DoCut
		{
			select(object)
			case player
			{
				if GameEvents[27] = 0
				{
					"I should sit down first."
				}
				else
				{
					remove blairsUltraDeath
					"I slice, erm. I don't know what it's called. It's near my thumb but not the thumb precisely.
					Maybe the fleshy part of the thumb? The meaty part of the thumb? The base of the thumb? The heel of
					the thumb? The butt of the thumb? The pulpy part of the thumb? The muscley part of the thumb?\n"

					"I guess the ball of the thumb, anyway, I slice a three inch gash on my left hand. Blood gushes out
					and because there's ice all over the floor and on every single surface on this moon it starts to bounce,
					like it does on hockey rinks.\n"

					"\"Hey!\" screams someone from the back, \"What are you doing?\"\n"

					"\"I've cut myself!\" I say convincingly. \"Ow!\""

					Activate(Doctor_Arrives_Knife)
				}
			}
			case else
			{
				"I don't know where the bouncer is, I think he's still near the entrance, but I doubt he's going to let me
				cut other things and people. ";

				if GameEvents[19] = 0
				{
					"Though if I could somehow inflict an injury, a doctor might come running..."
				}
				else {
					""
				}
			}
 		}
	}
}

object blairsUltraDeath "Blair's Ultra Death"
{
	nouns "ultra" "death" "sauce" "bottle"
	adjective "blair" "blair's" "hot" "death"
	article "the"
	in HotSauceBar

	short_desc
	{
		"Sitting by itself on the bar is a bottle of Blair's Ultra Death sauce."
	}
	long_desc
	{
		"This particular bottle had been open before, it's missing the keychain with the skull that is attached to unopen ones. It contains about five ounces of sauce, the
		recipe has not changed in years. The cap is off, missing. It has a white human skull drawn on it. 'FEEL ALIVE,' it says, taunting all who read it."
	}
	before
	{
		object DoGet
		{
			if GameEvents[26] = 0
			{
				"I can't reach it, I'd have to talk to the bartender to order it."
				Bartender.talkValue = 2
			}
			else {
				move self to player
				"Got it."
			}
		}
		object DoDrink
		{
			if GameEvents[27] = 0
			{
				"I should sit down first."
			}
			else {

				move MiladNahas to location

				Bartender.talkValue = 999
				Captain.talkValue = 999
				Bouncer.talkValue = 999

				remove knife

				"I take the Blair's Sauce and turn 180 degrees on the bar stool. I unwrap it and drink the entire thing in one quick slurp.\n"
				"This was a terrible idea, probably the worst idea I've ever had.\n"
				"My eyes start to water, my x-ray specs inexplicably start scanning even though they are on top of my head and I start coughing and hiccuping. This is poison, there
				was a mixup or something, there is no way anyone could have ever put this into a bottle intentionally. It burns, it's burning through my face!\n"
				"\"Aye pal, one milk then, innit, won ye done wit pure greeting,\" asks(?) the bartender.\n"
				"I can't respond. I'm hunched over dying. This is it, I'm $%^&ing dying. Even the Captain has gone from an arched eyebrow to actual concern.\n"
				"A man emerges from the far booth. \"What did this flailing cretin do?\" he asks. I assume I am hallucinating, but he has a white lab coat and a stethoscope. So great, while
				this sauce is killing me I ended up finding a doctor either in full Enceladus City Comic Con Cosplay or a porn.\n"
				"\"He ingested a little too much of the hot sauce,\" says the Captain. She puts her hand on my forehead. At first I think she's trying to take my temperature and I feel
				like that was really motherly of her, but then I realize she was just trying to turn my specs off so I don't irradiate every living bit of tissue in the bar."
				"\n[Strike any key to continue]"
				pause
				"\nThe Doctor rips the two liters of milk that the bartender produced out of her hands and splashes me in the face with it. He forces me to drink about a quarter of the jug.
				It's not really helping anything past my throat, but I think I'm not dying any more.\n"
				"\"Thanks, Doc,\" I try to say, but what comes out it something more akin to, 'Quaalude Farsase.'"

				Activate(JaRod_Hallucinates)
			}
		}
	}
}

object mineKey "key"
{
	nouns "key"
	adjective "mine"
	article "the"

	long_desc
	{
		"I think it's made out of solid brass or a similarly heavy combo of metals, but it's painted black, the paint having started to chip."
	}
	before
	{
		object DoDrop
		{
			"Look, I'm just trying to do a good job here and throwing the key around isn't going to help me accomplish that."
		}
	}
}

vehicle mineCart "mine cart"
{
nouns "cart"
adjective "mine"
article "the"
parse_rank 1

in mineLevelThree

		long_desc
		{
			"It's more of a lift instead of a cart, but regardless three beings can easily fit inside and it would take us up if we wanted
			to get into it and go that way."
		}
		before
		{
			object DoEnter
			{
				if GameEvents[20] = 0
				{
					"Nobody else seems to want to get into it and I don't want to be weird, so I don't do that."
				}
				else {
					"The three of us hop into the mine cart and are taken to the top ... "
					"\n[Strike any key to continue]"
					pause
					move Mailx to EntranceToMine
					move Captain to EntranceToMine
					mineDoor is not locked
					mineDoor is open
					MovePlayer(EntranceToMine)
				}

			}
			object DoGet
			{
				"The mine cart is physically attached to rails in the mine, so I cannot do that."
			}
		}
}

object silverOre "silver ore"
{
nouns "ore" "silver"
adjective "silver"
article "the"

	long_desc
	{
		"A baseball-sized chunk of silver ore, not the purest thing in the world, mind you, but pure enough for what I suspect
		will be our purposes."
	}
	before
	{
		object DoDrop
		{
			"I don't think dropping the ore is a good idea."
		}
		object DoThrowAt
		{
			select(xobject)
			case(werewolf)
			{
				!// Kill the werewolf
				"I hit the werewolf in the chest with the silver. The thing about the plastic genetic surgery that these people sign up for is that in
				order for the Live Action Role Playing to be accurate, they give themselves the weaknesses of the legendary creatures they want to be
				as well as the strengths.\n"
				"The chunk of silver ore goes right through the sternum of the werewolf, passing through like a metal skewer through marshmellow. The ore
				isn't even particularly slowed down by the werewolf's flesh and strikes the Plagoo, falling to the ground.\n"
				"The werewolf falls over, dead."

				remove silverOre
				werewolf is not living

				GameEvents[21] = 1
			}
			case(McGrathley)
			{
				"I try to throw the silver ore at McGrathley but he has his weapon ready by the time it softly arcs toward him. He blasts it out
				of the sky and laughs at his accomplishment in triumph."

				remove silverOre
			}
			case(Captain)
			{
				"She's on my side! How would that help?"
			}
			case(Mailx)
			{
				"I get frustrated with computers as well, but not enough to beat him with some ore!"
			}
			case(snowmobile)
			{
				"There is no reason for the snowmobile to be any sort of target for me!"
			}
			case(Plagoo)
			{
				"The werewolf is coming at me fast and I panic! I end up accidentally trying a bank shot off the Plagoo!\n"

				"I hit the werewolf in the chest with the silver. The thing about the plastic genetic surgery that these people sign up for is that in
				order for the Live Action Role Playing to be accurate, they give themselves the weaknesses of the legendary creatures they want to be
				as well as the strengths.\n"
				"The chunk of silver ore goes right through the sternum of the werewolf, passing through like a metal skewer through marshmellow. The ore
				isn't even particularly slowed down by the werewolf's flesh and strikes the Plagoo, falling to the ground.\n"
				"The werewolf falls over, dead.\n"

				"\"Ha ha!\" says Finnian through the ship's comm from inside. \"Great shot, Butler!\""

				werewolf is not living

				remove silverOre

				GameEvents[21] = 1

			}
			case else
			{
				"That is not a very good target for me!"
			}
		}
	}
}

object aluminum "aluminum"
{
nouns "aluminum"
article "the"

	long_desc
	{
		"This is a large chunk of alumnium, which presumably we can use to patch up the ship."
	}
	before
	{
		object DoDrop
		{
			"I can't, I need it to help repair the ship."
		}
		object DoThrowAt
		{
			"It's too weirdly shaped to throw!"
		}
	}
}

vehicle snowmobile "snowmobile"
{
nouns "snowmobile"
article "the"
in CenterOfTown

		short_desc
		{
			"\nAbandoned just a bit beyond us is what appears to be a broken snowmobile."
		}

		long_desc
		{
			"Arctic Cat was a company that made snowmobiles all the way back in the 1970s and I think somehow someone dragged one through
			space and time and then left it in barely running condition, from the looks of it, 70 light minutes away on a Saturian moon."
		}
		before{
			object DoEnter, DoDrive
			{
				if  GameEvents[20] = 0
				{
					"I don't have any interest in that old piece of moon garbage."
				}
				else {
					!// Captain, Ja'Rod and Mailx all go to the Plagoo...

					move Captain to OutsideTheShip
					move Mailx to OutsideTheShip
					move werewolf to OutsideTheShip
					move McGrathley to OutsideTheShip
					OutsideTheShip is visited
					MovePlayer(OutsideTheShip)

				}
			}
			object DoGet
			{
				"It's too heavy for me to carry, even with Enceladus's decreased gravity!"
			}
		}
}

object iceBoulder "ice"
{
nouns "ice" "boulder"
adjectives "ice"
article "the"

	short_desc
	{
		"There is a boulder of ice here that is preventing exit from the room."
	}

	long_desc
	{
		"It's impossibly large and dense, too big for me to manipulate or chip away at without freezing to death. It covers
		the entirety of the way we came in."
	}
	before
	{
		object DoGet, DoMove, DoThrowAt, DoKick, DoHit
		{
			"It's too heavy for me to personally manipulate."
		}
	}
}

object Plagoo "HMCS Plagoo"
{
nouns "plagoo"
adjectives "hmcs"
article "the"

	long_desc
	{
		"The HMCS Plagoo is a small spaceship that can achieve 99.99% of light speed to move around the solar system. It is green in color, mostly,
		with yellow highlights and the escape module at the top of the ship is a crimson red."
	}
	before
	{
		object DoEnter
		{
			"I can't get over there right now."
		}
		object DoGet
		{
			"It's a spaceship, waaay too busy for me to carry."
		}
	}
}

object refreshingBeverage "gintata"
{
	nouns "gintata" "gin" "glass" "container" "alcohol"
	article "the"
	parse_rank 1

	long_desc
	{
		"Ah, the gintata! This is a drink of my own invention. You put as much gin into a gallon bucket as will
		fit and then fill the rest up with a flavor of Kool-Aid. You take a mason jar and dunk it into the bucket
		and drink it. Have a couple of these  and that's it for you for the rest of the evening, ta-ta."
	}
	before
	{
		object DoDrink
		{
			Final_Scene()
		}
		object DoRaise
		{
			"I raise this perfect drink, this perfect specimen of alcohol with the others before drinking it."
		}
	}
}

scenery tooth "tooth"
{
	nouns "tooth" "teeth"
	article "my"

	long_desc
	{
		"Well, I can't save it now. I really gotta start adding limes to everything I eat, though."
	}
}

object thong "thong"
{
	nouns "banana" "thong"
	adjective "fur-lined"
	article "Finnian's"
	in Finnian
	is hidden

	long_desc
	{
		"I only really take a quick, passing glance. Like I was looking at something in that general direction. Look, this isn't the craziest outfit that
		Finnian has reported to stand-up with."
	}
	before
	{
		object DoGet
		{
			"Ha! Not on my life!!"
		}
	}
}

object dunceCap "dunce cap"
{
	nouns "cap"
	adjective "dunce"
	article "the"
	is hidden

	long_desc
	{
		"It looks like a traffic cone painted white with the word DUNCE handwritten from the top down. I don't even know where he gets these things, much less
		why he'd ever want to wear one."
	}
	before
	{
		object DoGet
		{
			"I can't, first he's much taller than I am and secondly I don't really have anything I want to do with it."
		}
	}
}


object furCoat "fur coat"
{
	nouns "coat"
	adjective "fur" "lynx"
	article "the"
	is hidden

	long_desc
	{
		"It's beautiful, pearl and shade-of-night marbled patterns, runs from the neck down to the ankles. You'd have to be a real prick to order one new. Finnian does check out
		the antique shops every time we touch down somewhere, so maybe the former, but he also has a current subscription to the 'Skymall' of starships, which is also 'Skymall,'
		so extra-maybe the latter."
	}
	before
	{
		object DoGet
		{
			"It's too big for me. To take. And to wear. Both, really."
		}
	}
}

object airJordans "Air Jordans"
{
	nouns "jordans"
	adjectives "air"
	article "Finnian's"
	is hidden, plural

	long_desc
	{
		"Size 12 red and white basketball sneakers, presumably re-issues, but considering the other things that Finnian has worn since I have known him, I wouldn't
		bet on it."
	}
	before
	{
		object DoGet
		{
			"I think the last thing the solar system needs is me contributing to some sort of working, in-progress
			stereotype of Ethiopian Ensigns stealing space sneakers."
		}
	}
}

scenery miningRope "rope"
{
	nouns "rope"
	article "the"
	found_in mine mineLevelTwo mineLevelThree

	long_desc
	{
		"It looks kind of old and weathered, which is odd, as presumably they only shipped new stuff to Enceladus from
		off-world."
	}
	before
	{
		object DoClimb
		{
			Perform(&DoMove, d_obj)
		}
	}
}
